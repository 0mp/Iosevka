
### Combining marks

local markExtend [ACCENTX * 0.5]
local markStress : 0.5 * [Math.min STROKE [ACCENT * 0.6]]
local markFine : markStress * 0.8
local markHalfStroke : mix markFine markStress 0.5
local markMiddle [-MIDDLE]
local markDotsRadius : DOTRADIUS * markStress / HALFSTROKE

local aboveMarkTop [XH + ACCENT * 1.375]
local aboveMarkBot [XH + ACCENT * 0.35]

local belowMarkBot [0 - ACCENT * 1.375]
local belowMarkTop [0 - ACCENT * 0.35]


### Above marks

create-glyph 'dotAbove' : glyph-construction {
	set-width 0
	assign-unicode 0x307
	set-anchor 'above' MARK markMiddle XH markMiddle aboveMarkTop
	include : list {
		Ring [XH + ACCENT + DOTRADIUS] [XH + ACCENT - DOTRADIUS] [markMiddle - DOTRADIUS] [markMiddle + DOTRADIUS]
	}
}
create-glyph 'dieresisAbove' : glyph-construction {
	set-width 0
	assign-unicode 0x308
	set-anchor 'above' MARK markMiddle XH markMiddle aboveMarkTop
	include : list {
		Ring [XH + ACCENT + markDotsRadius] [XH + ACCENT - markDotsRadius] [markMiddle - markDotsRadius - markExtend] [markMiddle + markDotsRadius - markExtend]
		Ring [XH + ACCENT + markDotsRadius] [XH + ACCENT - markDotsRadius] [markMiddle - markDotsRadius + markExtend] [markMiddle + markDotsRadius + markExtend]
	}
}
create-glyph 'ringAbove' : glyph-construction {
	set-width 0
	assign-unicode 0x30A
	set-anchor 'above' MARK markMiddle XH markMiddle aboveMarkTop
	
	local radiusOut : DOTRADIUS * 1.5
	local radiusIn  : DOTRADIUS * 0.7
	include : list {
		Ring [XH + ACCENT + radiusOut] [XH + ACCENT - radiusOut] [markMiddle - radiusOut] [markMiddle + radiusOut]
		Ring [XH + ACCENT + radiusIn] [XH + ACCENT - radiusIn] [markMiddle - radiusIn] [markMiddle + radiusIn]
	}
	reverse-last
}
create-glyph 'graveAbove' : glyph-construction {
	set-width 0
	assign-unicode 0x300
	set-anchor 'above' MARK markMiddle XH markMiddle aboveMarkTop
	
	include : create-stroke
		:.start-from [markMiddle + markStress] aboveMarkBot
		:.set-width  markFine markFine
		:.line-to    [markMiddle - markExtend] aboveMarkTop
		:.set-width  markStress markStress
}
create-glyph 'acuteAbove' : glyph-construction {
	set-width 0
	assign-unicode 0x301
	set-anchor 'above' MARK markMiddle XH markMiddle aboveMarkTop
	
	include : create-stroke
		:.start-from [markMiddle - markStress] aboveMarkBot
		:.set-width  markFine markFine
		:.line-to    [markMiddle + markExtend] aboveMarkTop
		:.set-width  markStress markStress
}
create-glyph 'circumflexAbove' : glyph-construction {
	set-width 0
	assign-unicode 0x302
	set-anchor 'above' MARK markMiddle XH markMiddle aboveMarkTop
	
	include : create-stroke
		:.start-from [markMiddle - markExtend - markStress] [aboveMarkBot + markStress - markFine]
		:.set-width  markStress markStress
		:.line-to    markMiddle [aboveMarkTop + markFine * 0.7]
		:.heads-to   UPWARD
		:.set-samples    1
	
	include : create-stroke
		:.start-from [markMiddle + markExtend + markStress] [aboveMarkBot + markStress - markFine]
		:.set-width  markStress markStress
		:.line-to    markMiddle [aboveMarkTop + markFine * 0.7]
		:.heads-to   UPWARD
		:.set-samples    1
}
create-glyph 'caronAbove' : glyph-construction {
	set-width 0
	assign-unicode 0x30c
	set-anchor 'above' MARK markMiddle XH markMiddle aboveMarkTop
	
	include : create-stroke
		:.start-from [markMiddle - markExtend - markStress] aboveMarkTop
		:.set-width  markStress markStress
		:.line-to    markMiddle [aboveMarkBot - markFine * 1.7 + markStress]
		:.heads-to   DOWNWARD
		:.set-samples    1
	
	include : create-stroke
		:.start-from [markMiddle + markExtend + markStress] aboveMarkTop
		:.set-width  markStress markStress
		:.line-to    markMiddle [aboveMarkBot - markFine * 1.7 + markStress]
		:.heads-to   DOWNWARD
		:.set-samples    1
}
create-glyph 'tildeAbove' : glyph-construction {
	set-width 0
	assign-unicode 0x303
	set-anchor 'above' MARK markMiddle XH markMiddle aboveMarkTop
	
	local leftEnd [markMiddle - markExtend * 1.5]
	local rightEnd [markMiddle + markExtend * 1.5]
	
	local ttop aboveMarkTop
	local tbot : aboveMarkBot + markFine / 2
	local top : ttop + [markFine * 2]
	local bot : tbot - [markFine * 2]
	
	local tildeWave [linreg 40 1.52 52 1.33 markStress]
	local tildeWaveX 0.52
	local tildeWaveEnd 0
	
	include : create-stroke
		:.start-from leftEnd [mix tbot ttop tildeWaveEnd]
		:.set-width  markStress markStress
		:.cubic-to   [mix leftEnd rightEnd tildeWaveX] [mix bot top tildeWave] [mix leftEnd rightEnd [1 - tildeWaveX]] [mix bot top [1 - tildeWave]] rightEnd [mix tbot ttop [1 - tildeWaveEnd]]
		:.set-samples    11
}
create-glyph 'macronAbove' : glyph-construction {
	set-width 0
	assign-unicode 0x304
	set-anchor 'above' MARK markMiddle XH markMiddle aboveMarkTop
	
	local leftEnd [markMiddle - markExtend * 1.5]
	local rightEnd [markMiddle + markExtend * 1.5]
	
	include : create-stroke
		:.start-from leftEnd [aboveMarkTop - DOTRADIUS]
		:.set-width  markHalfStroke markHalfStroke
		:.heads-to   RIGHTWARD
		:.line-to    rightEnd [aboveMarkTop - DOTRADIUS]
		:.heads-to   RIGHTWARD
}


### Below marks
create-glyph 'dotBelow' : glyph-construction {
	set-width 0
	assign-unicode 0x323
	set-anchor 'below' MARK markMiddle 0 markMiddle belowMarkBot
	include : list {
		Ring [0 - ACCENT + DOTRADIUS] [0 - ACCENT - DOTRADIUS] [markMiddle - DOTRADIUS] [markMiddle + DOTRADIUS]
	}
}