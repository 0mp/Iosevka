###### GEOMETRIC SHAPES

### Blocks
create-glyph 'branch' : glyph-construction {
	assign-unicode 0xE0A0
	local fine : adviceBlackness 3.5
	local vBarPos : mix SB RIGHTSB 0.2
	local xRightBall : RIGHTSB - PERIODRADIUS
	local yRightBall : mix parenBot parenTop 0.45
	include : VBar vBarPos [parenBot + PERIODSIZE] [linreg SB parenTop xRightBall yRightBall vBarPos] fine
	include : create-stroke
		:.start-from SB parenTop
		:.set-width  [fine / 2] [fine / 2]
		:.line-to    xRightBall yRightBall
	include : list {
		RingAt vBarPos [parenBot + PERIODSIZE] PERIODRADIUS
		RingAt xRightBall yRightBall PERIODRADIUS
	}
}

### POWERLINE glyphs
create-glyph 'pwlTriangleRight' : glyph-construction {
	assign-unicode 0xE0B0
	start-from O font.hhea.ascent
	line-to    0 font.hhea.ascent
	line-to    WIDTH [mix font.hhea.ascent font.hhea.descent 0.5]
	line-to    0 font.hhea.descent
	line-to    O font.hhea.descent
}
create-glyph 'pwlAngleRight' : glyph-construction {
	assign-unicode 0xE0B1
	include : create-stroke
		:.start-from 0 font.hhea.ascent
		:.set-width  [HALFSTROKE * 0.5] [HALFSTROKE * 0.5]
		:.line-to    WIDTH [mix font.hhea.ascent font.hhea.descent 0.5]
		:.line-to    0 font.hhea.descent
}
create-glyph 'pwlTriangleLeft' : glyph-construction {
	assign-unicode 0xE0B2
	include glyphs.pwlTriangleRight
	include : FlipAround MIDDLE [mix font.hhea.ascent font.hhea.descent 0.5]
}
create-glyph 'pwlAngleLeft' : glyph-construction {
	assign-unicode 0xE0B3
	include glyphs.pwlAngleRight
	include : FlipAround MIDDLE [mix font.hhea.ascent font.hhea.descent 0.5]
}
create-glyph 'fullblock' : glyph-construction {
	assign-unicode 0x2588
	start-from 0 font.hhea.ascent
	line-to    WIDTH font.hhea.ascent
	line-to    WIDTH font.hhea.descent
	line-to    0 font.hhea.descent
}
create-glyph 'pwlLock' : glyph-construction {
	assign-unicode 0xE0A2
	local fine : adviceBlackness 3.5
	include glyphs.O
	start-from SB parenMid
	line-to    RIGHTSB parenMid
	line-to    RIGHTSB parenBot
	line-to    SB parenBot
}