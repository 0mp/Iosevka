### Extended cyrillic glyphs

create-glyph 'cyrYe' : glyph-construction
	assign-unicode 0x404
	include capitalMarks
	include glyphs.C
	include : HBar SB [mix RIGHTSB SB 0.25] (CAP / 2)

create-glyph 'cyrye' : glyph-construction
	assign-unicode 0x454
	include eMarks
	include glyphs.c
	include : HBar SB [mix RIGHTSB SB 0.25] (XH / 2)

define [CyrDzheShape top] : glyph-construction
	include : VBarLeft SB 0 top
	include : HBarBottom SB RIGHTSB 0
	include : VBarRight RIGHTSB 0 top
	include : VBar MIDDLE DESCENDER STROKE

create-glyph 'cyrDzhe' : glyph-construction
	assign-unicode 0x40F
	include ifMarks
	include : CyrDzheShape CAP

create-glyph 'cyrdzhe' : glyph-construction
	assign-unicode 0x45F
	include pMarks
	include : CyrDzheShape XH

define [CyrLjeShape top] : glyph-construction
	local xlefttop : mix SB RIGHTSB 0.075
	local jut : JUT * 0.72
	include : LegShape xlefttop [mix SB RIGHTSB 0.025] [mix SB 0 [if SLAB 0.75 0.5]] top 0 MVERTSTROKE
	include : CyrYeriShape top (MIDDLE - MVERTSTROKE / 2 * CORRECTION_HX) (RIGHTSB - O) MVERTSTROKE jut
	include : HBarTop xlefttop MIDDLE top
	if SLAB : begin
		include : LeftwardTopSerif xlefttop top (jut - MVERTSTROKE / 2 * CORRECTION_HX)

create-glyph 'cyrLje' : glyph-construction
	assign-unicode 0x409
	include capitalMarks
	include : CyrLjeShape CAP

create-glyph 'cyrlje' : glyph-construction
	assign-unicode 0x459
	include eMarks
	include : CyrLjeShape XH

define [CyrNjeShape top] : glyph-construction
	local xlefttop : mix SB RIGHTSB 0.075
	local jut : JUT * 0.72
	include : VBarLeft SB 0 top MVERTSTROKE
	include : CyrYeriShape top (MIDDLE - MVERTSTROKE / 2 * CORRECTION_HX) (RIGHTSB - O) MVERTSTROKE jut
	include : HBar (SB + MVERTSTROKE * 0.1) MIDDLE (top / 2)
	if SLAB : begin
		include : CenterTopSerif (SB + MVERTSTROKE / 2 * CORRECTION_HX) top jut
		include : CenterBottomSerif (SB + MVERTSTROKE / 2 * CORRECTION_HX) 0 jut

create-glyph 'cyrNje' : glyph-construction
	assign-unicode 0x40A
	include capitalMarks
	include : CyrNjeShape CAP

create-glyph 'cyrnje' : glyph-construction
	assign-unicode 0x45A
	include eMarks
	include : CyrNjeShape XH

create-glyph 'cyrTshe' : glyph-construction
	assign-unicode 0x40B
	include capitalMarks
	
	local left : mix SB RIGHTSB 0.15
	
	include : nShoulder (left + STROKE * CORRECTION_HX) RIGHTSB SHOULDERFINE
	include : VBarLeft left 0 CAP
	include : HBarTop SB [mix left RIGHTSB 0.5] CAP

alias 'cyrtshe' 0x45B 'hbar'

create-glyph 'cyrDje' : glyph-construction
	assign-unicode 0x402
	include capitalMarks
	
	local left : mix SB RIGHTSB 0.15
	
	include : nShoulder (left + STROKE * CORRECTION_HX) RIGHTSB SHOULDERFINE XH (HOOK + HALFSTROKE + O) SMOOTHA SMOOTHB
	include : VBarLeft left 0 CAP
	include : HBarTop SB [mix left RIGHTSB 0.5] CAP
	include : VerticalHook (RIGHTSB - HALFSTROKE * CORRECTION_HX) (HOOK + HALFSTROKE + O) [Math.max ((left - RIGHTSB) / 2 + HALFSTROKE) (-HOOK * 1.2)] HOOK

create-glyph 'cyrdje' : glyph-construction
	assign-unicode 0x452
	include ifMarks
	include glyphs.cyrtshe
	eject-contour 'serifRB'
	include : VerticalHook (RIGHTSB - HALFSTROKE * CORRECTION_HX) 0 (-HOOK * 1.2) HOOK


define [CyrGeShape top] : glyph-construction
	include : GammaShape top
	eject-contour 'serifRT'
	include : VBarRight (RIGHTSB - OX) top (top + ACCENT)
	reverse-last

create-glyph 'cyrGe' : glyph-construction
	assign-unicode 0x490
	include capitalMarks
	include : CyrGeShape CAP

create-glyph 'cyrge' : glyph-construction
	assign-unicode 0x491
	include eMarks
	include : CyrGeShape XH

create-glyph 'cyrIzhitsa' : glyph-construction
	assign-unicode 0x474
	include capitalMarks
	include : VHooktopShape CAP

create-glyph 'cyrue' : glyph-construction
	assign-unicode 0x4AF
	include pMarks
	include : YShape XH DESCENDER

alias 'Palochka' 0x4C0 'I'
alias 'palochka' 0x4CF 'l'
alias 'cyrAE' 0x4D4 'AE'
alias 'cyrae' 0x4D5 'ae'
alias 'cyrOe' 0x4E8 'Obar'
alias 'cyroe' 0x4E9 'obar'
alias 'cyrSchwa' 0x4D8 'turnSmallE'
alias 'cyrschwa' 0x4D9 'schwa'
alias 'cyrEzh' 0x4E0 'Ezh'
alias 'cyrezh' 0x4E1 'ezh'
alias 'cyrizhitsa' 0x475 'vhooktop'
alias 'cyrUe' 0x4AE 'Y'
alias 'cyrQa' 0x51A 'Q'
alias 'cyrqa' 0x51B 'q'
alias 'cyrWe' 0x51C 'W'
alias 'cyrwe' 0x51D 'w'


composite nothing 0x4A2 { glyphs.cyrEn [CyrDescender RIGHTSB] }
composite nothing 0x4A3 { glyphs.cyren [CyrDescender RIGHTSB] }
composite nothing 0x4B2 { glyphs.cyrHa [CyrDescender RIGHTSB] }
composite nothing 0x4B3 { glyphs.cyrha [CyrDescender RIGHTSB] }
composite nothing 0x4B6 { glyphs.cyrChe [CyrDescender RIGHTSB] }
composite nothing 0x4B7 { glyphs.cyrche [CyrDescender RIGHTSB] }
composite nothing 0x49A { glyphs.cyrKa [CyrDescender (RIGHTSB - O)] }
composite nothing 0x49B { glyphs.cyrka [CyrDescender (RIGHTSB - O)] }
composite nothing 0x4AC { glyphs.cyrTe [CyrDescender (MIDDLE + HALFSTROKE * CORRECTION_HX)] ifMarks}
composite nothing 0x4AD { glyphs.cyrte [CyrDescender (MIDDLE + HALFSTROKE * CORRECTION_HX)] pMarks}
composite nothing 0x496 { glyphs.cyrZhe [CyrDescender (RIGHTSB - O * 4)] }
composite nothing 0x497 { glyphs.cyrzhe [CyrDescender (RIGHTSB - O * 4)] }