### Extended cyrillic glyphs

create-glyph 'cyrYe' : glyph-construction {
	assign-unicode 0x404
	include capitalMarks
	include glyphs.C
	include : HBar SB [mix RIGHTSB SB 0.25] [CAP / 2]
}
create-glyph 'cyrye' : glyph-construction {
	assign-unicode 0x454
	include eMarks
	include glyphs.c
	include : HBar SB [mix RIGHTSB SB 0.25] [XH / 2]
}
define [CyrDzheShape top] : glyph-construction {
	include : VBarLeft SB 0 top
	include : HBarBottom SB RIGHTSB 0
	include : VBarRight RIGHTSB 0 top
	include : VBar MIDDLE DESCENDER STROKE
}
create-glyph 'cyrDzhe' : glyph-construction {
	assign-unicode 0x40F
	include ifMarks
	include : CyrDzheShape CAP
}
create-glyph 'cyrdzhe' : glyph-construction {
	assign-unicode 0x45F
	include pMarks
	include : CyrDzheShape XH
}
define [CyrLjeShape top] : glyph-construction {
	local fine : adviceBlackness 3.5
	local xlefttop : mix SB RIGHTSB 0.075
	include : LegShape xlefttop [mix SB RIGHTSB 0.025] [mix SB 0 0.25] top 0 fine
	include : CyrYeriShape top [MIDDLE - fine / 2 * ITALICCOR] RIGHTSB fine
	include : HBarTop xlefttop MIDDLE top
}
create-glyph 'cyrLje' : glyph-construction {
	assign-unicode 0x409
	include capitalMarks
	include : CyrLjeShape CAP
}
create-glyph 'cyrlje' : glyph-construction {
	assign-unicode 0x459
	include eMarks
	include : CyrLjeShape XH
}
define [CyrNjeShape top] : glyph-construction {
	local fine : adviceBlackness 3.5
	local xlefttop : mix SB RIGHTSB 0.075
	include : VBarLeft SB 0 top fine
	include : CyrYeriShape top [MIDDLE - fine / 2 * ITALICCOR] RIGHTSB fine
	include : HBar [SB + fine * 0.1] MIDDLE [top / 2]
}
create-glyph 'cyrNje' : glyph-construction {
	assign-unicode 0x40A
	include capitalMarks
	include : CyrNjeShape CAP
}
create-glyph 'cyrnje' : glyph-construction {
	assign-unicode 0x45A
	include eMarks
	include : CyrNjeShape XH
}
create-glyph 'cyrTshe' : glyph-construction {
	assign-unicode 0x40B
	include capitalMarks
	
	local left : mix SB RIGHTSB 0.15
	
	include : nBowl [left + STROKE * ITALICCOR] [mix left RIGHTSB 0.5] RIGHTSB [STROKE * 0.4]
	include : VBarLeft left 0 CAP
	include : HBarTop SB [mix left RIGHTSB 0.5] CAP
}
alias 'cyrtshe' 0x45B 'hbar'

create-glyph 'cyrDje' : glyph-construction {
	assign-unicode 0x402
	include capitalMarks
	
	local left : mix SB RIGHTSB 0.15
	
	include : nBowl [left + STROKE * ITALICCOR] [mix left RIGHTSB 0.5] RIGHTSB [STROKE * 0.4] XH [HOOK + HALFSTROKE + O] SMOOTHA SMOOTHB
	include : VBarLeft left 0 CAP
	include : HBarTop SB [mix left RIGHTSB 0.5] CAP
	include : VerticalHook [RIGHTSB - HALFSTROKE * ITALICCOR] [HOOK + HALFSTROKE + O] [Math.max [[left - RIGHTSB] / 2 + HALFSTROKE] [-HOOK * 1.2]] HOOK
}
create-glyph 'cyrdje' : glyph-construction {
	assign-unicode 0x452
	include ifMarks
	include glyphs.cyrtshe
	include : VerticalHook [RIGHTSB - HALFSTROKE * ITALICCOR] 0 [-HOOK * 1.2] HOOK
}

define [CyrGeShape top] : glyph-construction {
	include : LShape top
	include : FlipAround MIDDLE [top / 2] 1 [-1]
	include : VBarRight RIGHTSB top [top + ACCENT]
	reverse-last
}
create-glyph 'cyrGe' : glyph-construction {
	assign-unicode 0x490
	include capitalMarks
	include : CyrGeShape CAP
}
create-glyph 'cyrge' : glyph-construction {
	assign-unicode 0x491
	include eMarks
	include : CyrGeShape XH
}