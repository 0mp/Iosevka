### Extended cyrillic glyphs

create-glyph 'Ye' : glyph-construction {
	assign-unicode 0x404
	include capitalMarks
	include glyphs.C
	include : HBar SB [mix RIGHTSB SB 0.25] [CAP / 2]
}
create-glyph 'ye' : glyph-construction {
	assign-unicode 0x454
	include eMarks
	include glyphs.c
	include : HBar SB [mix RIGHTSB SB 0.25] [XH / 2]
}
define [DzheShape top] : glyph-construction {
	include : VBarLeft SB 0 top
	include : HBarBottom SB RIGHTSB 0
	include : VBarRight RIGHTSB 0 top
	include : VBar MIDDLE DESCENDER STROKE
}
create-glyph 'Dzhe' : glyph-construction {
	assign-unicode 0x40F
	include ifMarks
	include : DzheShape CAP
}
create-glyph 'dzhe' : glyph-construction {
	assign-unicode 0x45F
	include pMarks
	include : DzheShape XH
}
define [LjeShape top] : glyph-construction {
	local fine : adviceBlackness 3.5
	local xlefttop : mix SB RIGHTSB 0.075
	include : LegShape xlefttop [mix SB RIGHTSB 0.025] [mix SB 0 0.25] top 0 fine
	include : YeriShape top [MIDDLE - fine / 2 * ITALICCOR] RIGHTSB fine
	include : HBarTop xlefttop MIDDLE top
}
create-glyph 'Lje' : glyph-construction {
	assign-unicode 0x409
	include capitalMarks
	include : LjeShape CAP
}
create-glyph 'lje' : glyph-construction {
	assign-unicode 0x459
	include eMarks
	include : LjeShape XH
}
define [NjeShape top] : glyph-construction {
	local fine : adviceBlackness 3.5
	local xlefttop : mix SB RIGHTSB 0.075
	include : VBarLeft SB 0 top fine
	include : YeriShape top [MIDDLE - fine / 2 * ITALICCOR] RIGHTSB fine
	include : HBar [SB + fine * 0.1] MIDDLE [top / 2]
}
create-glyph 'Nje' : glyph-construction {
	assign-unicode 0x40A
	include capitalMarks
	include : NjeShape CAP
}
create-glyph 'nje' : glyph-construction {
	assign-unicode 0x45A
	include eMarks
	include : NjeShape XH
}
create-glyph 'Tshe' : glyph-construction {
	assign-unicode 0x40B
	include capitalMarks
	
	local left : mix SB RIGHTSB 0.15
	
	include : nBowl [left + STROKE * ITALICCOR] [mix left RIGHTSB 0.5] RIGHTSB [STROKE * 0.4]
	include : VBarLeft left 0 CAP
	include : HBarTop SB [mix left RIGHTSB 0.5] CAP
}
alias 'tshe' 0x45B 'hbar'

create-glyph 'Dje' : glyph-construction {
	assign-unicode 0x402
	include capitalMarks
	
	local left : mix SB RIGHTSB 0.15
	
	include : nBowl [left + STROKE * ITALICCOR] [mix left RIGHTSB 0.5] RIGHTSB [STROKE * 0.4] XH [HOOK + HALFSTROKE + O] SMOOTHA SMOOTHB
	include : VBarLeft left 0 CAP
	include : HBarTop SB [mix left RIGHTSB 0.5] CAP
	include : VerticalHook [RIGHTSB - HALFSTROKE * ITALICCOR] [HOOK + HALFSTROKE + O] [Math.max [[left - RIGHTSB] / 2 + HALFSTROKE] [-HOOK * 1.2]] HOOK
}
create-glyph 'dje' : glyph-construction {
	assign-unicode 0x452
	include ifMarks
	include glyphs.tshe
	include : VerticalHook [RIGHTSB - HALFSTROKE * ITALICCOR] 0 [-HOOK * 1.2] HOOK
}

define [GeShape top] : glyph-construction {
	include : LShape top
	include : FlipAround MIDDLE [top / 2] 1 [-1]
	include : VBarRight RIGHTSB [top + ACCENT] top
}
create-glyph 'Ge' : glyph-construction {
	assign-unicode 0x490
	include capitalMarks
	include : GeShape CAP
}
create-glyph 'ge' : glyph-construction {
	assign-unicode 0x491
	include eMarks
	include : GeShape XH
}