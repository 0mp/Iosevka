### Extended symbols

create-glyph 'nbsp' : glyph-construction {
	assign-unicode 0xA0
	include glyphs.space
}

create-glyph 'cent' : glyph-construction {
	assign-unicode 0xA2
	include glyphs.c AS_BASE
	include : create-stroke 
		:.start-from MIDDLE [XH - HALFSTROKE] 
		:.set-width HALFSTROKE HALFSTROKE 
		:.line-to MIDDLE [XH - DESCENDER / 2]
	include : create-stroke
		:.start-from MIDDLE [DESCENDER / 2]
		:.set-width HALFSTROKE HALFSTROKE
		:.line-to MIDDLE HALFSTROKE
}
create-glyph 'yen' : glyph-construction {
	assign-unicode 0xA5
	include glyphs.Y AS_BASE
	include : hbar SB RIGHTSB [CAP * 0.45] [adviceBlackness 4]
	include : hbar SB RIGHTSB [CAP * 0.25] [adviceBlackness 4]
}

create-glyph 'section' : glyph-construction {
	assign-unicode 0xA7
	
	local top parenTop
	local bot parenBot
	local sma [SMOOTHA * 0.85]
	
	local extension1    0.5
	local extension2    0.625
	local extension3x   0.5
	local extension3y : mix extension1 [1 - sma / [top - bot]] 0.5
	
	include : XSHookUpper top SB MIDDLE RIGHTSB sma HOOK
	include : sStrand [top - sma] [mix bot top extension1]
	include : sStrand [mix top bot extension1] [bot + sma]
	include : XSHookLower bot SB MIDDLE RIGHTSB sma HOOK
	
	include : create-stroke
		:.start-from [SB + HALFSTROKE] [[mix top bot extension1] - HALFSTROKE * [globalTransform.yx * 0.985]]
		:.set-width  HALFSTROKE HALFSTROKE
		:.curve-to   [SB + HALFSTROKE] [mix bot top extension2] [mix [SB + HALFSTROKE] [RIGHTSB - HALFSTROKE] extension3x] [mix bot top extension3y]
	
	include : create-stroke
		:.start-from [RIGHTSB - HALFSTROKE] [[mix bot top extension1] + HALFSTROKE * [globalTransform.yx * 0.985]]
		:.set-width  HALFSTROKE HALFSTROKE
		:.curve-to   [RIGHTSB - HALFSTROKE] [mix top bot extension2] [mix [RIGHTSB - HALFSTROKE] [SB + HALFSTROKE] extension3x] [mix top bot extension3y]
}

create-glyph 'plusminus' : glyph-construction {
	assign-unicode 0xB1
	set-width WIDTH
	include glyphs.underscore
	include glyphs.plus
}

create-glyph 'exclamdown' : glyph-construction {
	assign-unicode 0xA1
	set-width WIDTH
	include glyphs.exclam
	apply-transform : Upright
	apply-transform : Translate [-MIDDLE] [-XH / 2]
	apply-transform : Scale [-1]
	apply-transform : Translate MIDDLE [XH / 2]
	apply-transform : Italify
}
create-glyph 'questiondown' : glyph-construction {
	assign-unicode 0xBF
	set-width WIDTH
	include glyphs.question
	apply-transform : Upright
	apply-transform : Translate [-MIDDLE] [-XH / 2]
	apply-transform : Scale [-1]
	apply-transform : Translate MIDDLE [XH / 2]
	apply-transform : Italify
}

create-glyph 'sterling' : glyph-construction {
	assign-unicode 0xA3
	set-width WIDTH
	include glyphs.'longs.upright'
	apply-transform : Translate [-WIDTH * 0.125] 0
	include : fBar [mix SB RIGHTSB 0.05] [mix SB RIGHTSB 0.7] [CAP * 0.5 + STROKE]
	include glyphs.underscore
}
create-glyph 'micro' : glyph-construction {
	set-width WIDTH
	assign-unicode 0xB5
	include pMarks
	include glyphs.u
	include : create-stroke
		:.start-from SB DESCENDER
		:.heads-to   UPWARD
		:.set-width  0 STROKE
		:.line-to    SB [DESCENDER / 2]
		:.heads-to   UPWARD
		:.line-to    SB SMALLSMOOTHB
		:.heads-to   UPWARD
		:.set-width  0 [adviceBlackness 4]
}
create-glyph 'softhyphen' : glyph-construction {
	assign-unicode 0xAD
	include glyphs.hyphen AS_BASE
}
create-glyph 'bullet' : glyph-construction {
	assign-unicode 0xB7
	include : list : Ring [parenMid + PERIODRADIUS - O] [parenMid - PERIODRADIUS + O] [MIDDLE - PERIODRADIUS + O] [MIDDLE + PERIODRADIUS - O]
}
create-glyph 'currency' : glyph-construction {
	assign-unicode 0xA4
	local sw : adviceBlackness 3.5
	local radius : [RIGHTSB - SB] / 2
	include : smallo [parenMid + radius * 0.9] [parenMid - radius * 0.9] [MIDDLE - radius * 0.9] [MIDDLE + radius * 0.9] sw
	foreach j [range 0 4] : begin {
		local angle : Math.PI * [j / 2 + 1 / 4]
		include : create-stroke
			:.start-from [MIDDLE + radius * 0.87 * [Math.cos angle]] [parenMid + radius * 0.87 * [Math.sin angle]]
			:.set-width [sw / 2] [sw / 2]
			:.line-to [MIDDLE + radius * [Math.sqrt 2] * [Math.cos angle]] [parenMid + radius * [Math.sqrt 2] * [Math.sin angle]] 
	}
}
create-glyph 'euro' : glyph-construction {
	assign-unicode 0x20AC
	include glyphs.C
	include : hbar [SB - HALFSTROKE] [mix SB RIGHTSB 0.7] [CAP * 0.4] [adviceBlackness 4]
	include : hbar [SB - HALFSTROKE] [mix SB RIGHTSB 0.7] [CAP * 0.6] [adviceBlackness 4]
}
create-glyph 'multiply' : glyph-construction {
	assign-unicode 0xD7
	local radius : [RIGHTSB - SB] / 2
	include : create-stroke
		:.start-from [MIDDLE - radius] [parenMid - radius]
		:.set-width  HALFSTROKE HALFSTROKE
		:.line-to    [MIDDLE + radius] [parenMid + radius]
	
	include : create-stroke
		:.start-from [MIDDLE + radius] [parenMid - radius]
		:.set-width  HALFSTROKE HALFSTROKE
		:.line-to    [MIDDLE - radius] [parenMid + radius]
}
create-glyph 'divide' : glyph-construction {
	assign-unicode 0xF7
	include glyphs.hyphen
	local radius : [RIGHTSB - SB] / 2
	include : list {
		Ring [parenMid + radius + DOTRADIUS] [parenMid + radius - DOTRADIUS] [MIDDLE - DOTRADIUS + O] [MIDDLE + DOTRADIUS - O]
		Ring [parenMid - radius + DOTRADIUS] [parenMid - radius - DOTRADIUS] [MIDDLE - DOTRADIUS + O] [MIDDLE + DOTRADIUS - O]
	}
}
create-glyph 'brokenbar' : glyph-construction {
	assign-unicode 0xA6
	include : vbar MIDDLE [parenMid + HALFSTROKE] parenTop
	include : vbar MIDDLE parenBot [parenMid - HALFSTROKE]
}
create-glyph 'singleguillemetleft' : glyph-construction {
	set-width WIDTH
	assign-unicode 0x2039
	
	local top : mix parenMid parenTop 0.5
	local bot : mix parenMid parenBot 0.5
	local extension 0.55
	local extension2x [-0.2]
	local extension2y 0.375
	local cor 1
	
	local halfsw : 0.5 * [adviceBlackness 3.5]
	
	include : create-stroke
		:.start-from  [mix MIDDLE RIGHTSB extension] top
		:.set-width   halfsw halfsw
		:.curve-to    [mix MIDDLE SB extension2x] [mix top bot extension2y] [mix MIDDLE SB extension] [mix top bot 0.5]
		:.set-width   [halfsw * cor] [halfsw * cor]
		:.heads-to    LEFTWARD
		:.set-samples 1
	
	include : create-stroke
		:.start-from  [mix MIDDLE SB extension] [mix top bot 0.5]
		:.set-width   [halfsw * cor] [halfsw * cor]
		:.heads-to    RIGHTWARD
		:.curve-to    [mix MIDDLE SB extension2x] [mix bot top extension2y] [mix MIDDLE RIGHTSB extension] bot
		:.set-width   halfsw halfsw
		:.set-samples 1
	
	apply-transform : Translate [-[RIGHTSB - SB] * 0.05] 0
}
create-glyph 'guillemetleft' : glyph-construction {
	set-width WIDTH
	assign-unicode 0xAB
	
	include glyphs.singleguillemetleft
	apply-transform : Translate [[RIGHTSB - SB] * 0.5] 0
	include glyphs.singleguillemetleft
	apply-transform : Translate [-[RIGHTSB - SB] * 0.25] 0
}

create-glyph 'singleguillemetright' : glyph-construction {
	set-width WIDTH
	assign-unicode 0x203A
	
	include glyphs.singleguillemetleft
	include : FlipAround MIDDLE parenMid
}
create-glyph 'guillemetright' : glyph-construction {
	set-width WIDTH
	assign-unicode 0xBB
	
	include glyphs.guillemetleft
	include : FlipAround MIDDLE parenMid
}

create-glyph 'paragraph' : glyph-construction {
	set-width WIDTH
	assign-unicode 0xB6
	local sw : adviceBlackness 3.6
	
	include : vbar [MIDDLE + sw / 4] 0 CAP sw
	include : vbar [RIGHTSB - sw / 2] 0 CAP sw
	include : create-stroke
		:.start-from [MIDDLE - sw / 4] [CAP - sw / 2]
		:.set-width  [sw / 2] [sw / 2]
		:.heads-to   LEFTWARD
		:.arc-hv-to  [SB + sw / 2] [mix [CAP * 0.5] [CAP - sw / 2] 0.5]
		:.heads-to   DOWNWARD
		:.arc-vh-to  [MIDDLE - sw / 4] [CAP * 0.5]
		:.heads-to   RIGHTWARD
}
create-glyph 'negate' : glyph-construction {
	assign-unicode 0xAC
	
	include glyphs.hyphen
	
	include : vbar [RIGHTSB - HALFSTROKE] [hyphenCenter - [RIGHTSB - SB] * 0.55] [hyphenCenter + HALFSTROKE]
}