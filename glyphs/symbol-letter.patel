###### Letter-like symbols

### Currency
create-glyph 'currency' : glyph-construction {
	assign-unicode 0xA4
	local sw : adviceBlackness 3.5
	local radius : [RIGHTSB - SB] / 2
	include : smallo [parenMid + radius * 0.9] [parenMid - radius * 0.9] [MIDDLE - radius * 0.9] [MIDDLE + radius * 0.9] sw
	foreach j [range 0 4] : begin {
		local angle : Math.PI * [j / 2 + 1 / 4]
		include : create-stroke
			:.start-from [MIDDLE + radius * 0.87 * [Math.cos angle]] [parenMid + radius * 0.87 * [Math.sin angle]]
			:.set-width [sw / 2] [sw / 2]
			:.line-to [MIDDLE + radius * [Math.sqrt 2] * [Math.cos angle]] [parenMid + radius * [Math.sqrt 2] * [Math.sin angle]] 
	}
}
create-glyph 'euro' : glyph-construction {
	assign-unicode 0x20AC
	include glyphs.C
	include : HBar [SB - HALFSTROKE] [mix SB RIGHTSB 0.7] [CAP * 0.4] [adviceBlackness 4]
	include : HBar [SB - HALFSTROKE] [mix SB RIGHTSB 0.7] [CAP * 0.6] [adviceBlackness 4]
}
create-glyph 'cent' : glyph-construction {
	assign-unicode 0xA2
	include glyphs.c AS_BASE
	include : create-stroke 
		:.start-from MIDDLE [XH - HALFSTROKE] 
		:.set-width HALFSTROKE HALFSTROKE 
		:.line-to MIDDLE [XH - DESCENDER / 2]
	include : create-stroke
		:.start-from MIDDLE [DESCENDER / 2]
		:.set-width HALFSTROKE HALFSTROKE
		:.line-to MIDDLE HALFSTROKE
}
create-glyph 'yen' : glyph-construction {
	assign-unicode 0xA5
	include glyphs.Y AS_BASE
	include : HBar SB RIGHTSB [CAP * 0.45] [adviceBlackness 4]
	include : HBar SB RIGHTSB [CAP * 0.25] [adviceBlackness 4]
}
create-glyph 'sterling' : glyph-construction {
	assign-unicode 0xA3
	set-width WIDTH
	include glyphs.'longs.upright'
	apply-transform : Translate [-WIDTH * 0.125] 0
	include : fBar [mix SB RIGHTSB 0.05] [mix SB RIGHTSB 0.7] [CAP * 0.5 + STROKE]
	include glyphs.underscore
}
create-glyph 'liraSymbol' : glyph-construction {
	assign-unicode 0x20A4
	set-width WIDTH
	include glyphs.'longs.upright'
	apply-transform : Translate [-WIDTH * 0.125] 0
	include : fBar [mix SB RIGHTSB 0.05] [mix SB RIGHTSB 0.7] [CAP * 0.35 + STROKE]
	include : fBar [mix SB RIGHTSB 0.05] [mix SB RIGHTSB 0.7] [CAP * 0.6 + STROKE]
	include glyphs.underscore
}
create-glyph 'frenchFranc' : glyph-construction {
	assign-unicode 0x20A3
	include glyphs.F
	include : HOverlayBar [mix SB 0 0.7] [mix SB RIGHTSB 0.5] [mix 0 XH 0.3]
}
create-glyph 'peseta' : glyph-construction {
	assign-unicode 0x20A7
	include glyphs.P
	include : HOverlayBar [mix SB 0 0.7] [mix RIGHTSB WIDTH 0.7] [mix CAPMIDDLE CAP 0.45]
}

### Other
create-glyph 'numero' : glyph-construction {
	assign-unicode 0x2116
	local fine : adviceBlackness 4.5
	include : smallo XH [fine * 2] [MIDDLE + fine / 2] [mix RIGHTSB WIDTH 0.5] fine [SMOOTHA * fine / STROKE] [SMOOTHB * fine / STROKE]
	include : NShape CAP [SB / 2] MIDDLE 4.5 5.5
	include : HBarBottom [MIDDLE + fine / 2] [mix RIGHTSB WIDTH 0.5] 0 fine
}
alias 'Ohm' 0x2126 'Omega'
turned 'Mho' 0x2127 'Ohm' MIDDLE [CAP / 2]

create-glyph 'ell' : glyph-construction {
	assign-unicode 0x2113
	include bMarks
	local fine : adviceBlackness 3.5
	local middle : MIDDLE - fine / 2
	local left : middle - fine * 1.25
	local right : middle + fine * 1.5
	local sma : SMOOTHA * 0.6
	local smb : SMOOTHB * 0.6
	include : spiro {
		widths.center fine
		g2   left [mix 0 CAP 0.33]
		quadcontrols 1 0.6
		g2   right [mix 0 CAP 0.8]
		arcvh
		g2   [mix middle right 0.5] [CAPO - fine / 2]
		archv
		flat middle [CAP - sma]
		curl middle [SMOOTHB * 0.5 + fine / 2]
		hookend [O + fine / 2]
		g2   [middle + fine * 2] SHOOK
	}
}
create-glyph 'estimated' : glyph-construction {
	assign-unicode 0x212E
	include : SmallEShape CAP [adviceBlackness 4] [0.5 - [adviceBlackness 4] / CAP / 2]
}