###### UPPER CASE

### A V X Y K
create-glyph 'A' : glyph-construction {
	set-width WIDTH
	assign-unicode 'A'
	include capitalMarks
	set-anchor 'trailing' BASE [RIGHTSB - markHalfStroke] 0
	local TURN [XH * 0.1]
	
	include : create-stroke
		:.start-from SB 0
		:.heads-to   UPWARD
		:.set-width  0 STROKE
		:.line-to    SB TURN
		:.heads-to   UPWARD
		:.curve-to   SB [mix TURN CAP 0.27] [MIDDLE - STROKE / 2] CAP
		:.set-width  0 [STROKE * 0.8]
	
	include : create-stroke
		:.start-from RIGHTSB 0
		:.heads-to   UPWARD
		:.set-width  STROKE 0
		:.line-to    RIGHTSB TURN
		:.heads-to   UPWARD
		:.curve-to   RIGHTSB [mix TURN CAP 0.27] [MIDDLE + STROKE / 2] CAP
		:.set-width  [STROKE * 0.8] 0
	
	include : create-stroke 
		:.start-from [SB + STROKE] [XH / 2]
		:.heads-to   RIGHTWARD
		:.set-width  0 STROKE
		:.line-to    [RIGHTSB - STROKE] [XH / 2]
		:.heads-to   RIGHTWARD
	
	#top cap
	start-from [MIDDLE - STROKE / 2] CAP
	line-to [MIDDLE + STROKE / 2] CAP
	line-to MIDDLE [CAP - STROKE]
}
create-glyph 'V' : glyph-construction {
	set-width WIDTH
	assign-unicode 'V'
	include capitalMarks
	
	local TURN [CAP * 0.9]
	
	include : create-stroke
		:.start-from SB CAP
		:.heads-to   DOWNWARD
		:.set-width  STROKE 0
		:.line-to    SB TURN
		:.heads-to   DOWNWARD
		:.curve-to   SB [[1 - 0.27] * TURN] [MIDDLE - STROKE / 2] 0
		:.set-width  [STROKE * 0.8] 0
	
	include : create-stroke
		:.start-from RIGHTSB CAP
		:.heads-to   DOWNWARD
		:.set-width  0 STROKE
		:.line-to    RIGHTSB TURN
		:.heads-to   DOWNWARD
		:.curve-to   RIGHTSB [[1 - 0.27] * TURN] [MIDDLE + STROKE / 2] 0
		:.set-width  0 [STROKE * 0.8]

	#bottom cap
	start-from [MIDDLE + STROKE / 2] 0
	line-to    [MIDDLE - STROKE / 2] 0
	line-to    MIDDLE STROKE
}
create-glyph 'W' : glyph-construction {
	set-width WIDTH
	assign-unicode 'W'
	include capitalMarks
	
	local TURN [CAP * 0.75]
	local turn2 [CAP * 0.59]
	local wheight [CAP * 0.6]
	local bottomStroke : adviceBlackness 5.2
	local m1 : WIDTH * 0.3
	local m2 : WIDTH * 0.7
	
	include : create-stroke
		:.start-from SB CAP
		:.heads-to DOWNWARD
		:.set-width STROKE 0
		:.line-to SB TURN
		:.heads-to DOWNWARD
		:.curve-to SB [[1 - 0.27] * TURN] [m1 - bottomStroke / 2] 0
		:.set-width bottomStroke 0
	
	include : create-stroke
		:.start-from RIGHTSB CAP
		:.heads-to DOWNWARD
		:.set-width 0 STROKE
		:.line-to RIGHTSB TURN
		:.heads-to DOWNWARD
		:.curve-to RIGHTSB [[1 - 0.27] * TURN] [m2 + bottomStroke / 2] 0
		:.set-width 0 bottomStroke
		
	include : create-stroke
		:.start-from [MIDDLE + bottomStroke / 2] wheight
		:.heads-to DOWNWARD
		:.set-width 0 bottomStroke
		:.line-to [MIDDLE + bottomStroke / 2] turn2
		:.heads-to DOWNWARD
		:.curve-to [MIDDLE + bottomStroke / 2] [[1 - 0.1] * turn2] [m1 + bottomStroke / 2] 0
		:.set-width 0 bottomStroke
	
	include : create-stroke
		:.start-from [MIDDLE - bottomStroke / 2] wheight
		:.heads-to DOWNWARD
		:.set-width bottomStroke 0
		:.line-to [MIDDLE - bottomStroke / 2] turn2
		:.heads-to DOWNWARD
		:.curve-to [MIDDLE - bottomStroke / 2] [[1 - 0.1] * turn2] [m2 - bottomStroke / 2] 0
		:.set-width bottomStroke 0

	#bottom cap
	start-from [m1 + bottomStroke / 2] 0
	line-to [m1 - bottomStroke / 2] 0
	line-to m1 bottomStroke
	
	start-from [m2 + bottomStroke / 2] 0
	line-to [m2 - bottomStroke / 2] 0
	line-to m2 bottomStroke
}
create-glyph 'X' : glyph-construction {
	set-width WIDTH
	assign-unicode 'X'
	include capitalMarks
	
	include : xStrand SB 0 RIGHTSB CAP 0.1 0.4 0.28
	include : xStrand SB CAP RIGHTSB 0 0.1 0.4 0.28
}
define [YShape top] : glyph-construction {
	local cross [top * 0.4]
	include : halfXStrand SB top MIDDLE cross 0.1 0.4 0.28
	include : halfXStrand RIGHTSB top MIDDLE cross 0.1 0.4 0.28
	include : create-stroke
		:.start-from MIDDLE 0
		:.set-width HALFSTROKE HALFSTROKE
		:.heads-to UPWARD
		:.line-to MIDDLE [cross + HALFSTROKE]
		:.heads-to UPWARD
}
create-glyph 'Y' : glyph-construction {
	set-width WIDTH
	assign-unicode 'Y'
	include capitalMarks
	include : YShape CAP
}

create-glyph 'K' : glyph-construction {
	set-width WIDTH
	assign-unicode 'K'
	include capitalMarks
	
	local TURN [CAP * 0.95]
	local rturn [XH * 0.1]
	local right [RIGHTSB - O]
	
	local fine : adviceBlackness 3.5
	
	include : create-stroke 
		:.start-from SB 0
		:.set-width  0 STROKE
		:.heads-to   UPWARD
		:.line-to    SB CAP
		:.heads-to   UPWARD
		
	include : create-stroke
		:.start-from RIGHTSB CAP
		:.heads-to   DOWNWARD
		:.set-width  0 STROKE
		:.line-to    RIGHTSB TURN
		:.heads-to   DOWNWARD
		:.curve-to   RIGHTSB [[1 - 0.18] * TURN] [SB + STROKE] [CAP * 0.35]
		:.set-width  0 fine
		
	include : create-stroke
		:.start-from [right - HALFSTROKE] 0
		:.heads-to UPWARD
		:.set-width HALFSTROKE HALFSTROKE
		:.curve-to [right - HALFSTROKE] [rturn + 0.2 * [XH - rturn]] MIDDLE [CAPMIDDLE + HALFSTROKE]
		:.set-width [fine / 2] [fine / 2]
		
}

### B D P R
define [BShape top] : glyph-construction {
	local bowl : top * 0.52 + HALFSTROKE
	local tkappa [COKAPPA - 0.22]
	local bkappa [COKAPPA - 0.2]
	
	local turntop : [top - [bowl - STROKE]] * 0.5
	local turnbottom : bowl * 0.45

	include : spiro {
		widths.rhs
		flat [SB - O] top [heading RIGHTWARD]
		curl [RIGHTSB - SB * 0.5 - turntop - OMIDCOR_S] top
		archv
		g4 [RIGHTSB - SB * 0.5 - O] [mix top [bowl - STROKE] [SMOOTHB / [SMOOTHA + SMOOTHB]]]
		arcvh
		flat [RIGHTSB - SB * 0.5 - turntop + OMIDCOR_S] [bowl - STROKE]
		curl [SB - O] [bowl - STROKE] [heading LEFTWARD]
	}
	include : spiro {
		widths.rhs
		flat [SB - O] bowl [heading RIGHTWARD]
		curl [RIGHTSB - turnbottom - OMIDCOR_S] bowl
		archv
		g4 [RIGHTSB - O] [mix bowl 0 [SMOOTHB / [SMOOTHA + SMOOTHB]]]
		arcvh
		flat [RIGHTSB - turnbottom + OMIDCOR_S] 0
		curl [SB - O] 0 [heading LEFTWARD]
	}
	include : VBarLeft SB 0 top
}
create-glyph 'B' : glyph-construction {
	set-width WIDTH
	assign-unicode 'B'
	include capitalMarks
	include : BShape CAP
	#throw 'www'
}

create-glyph 'D' : glyph-construction {
	set-width WIDTH
	assign-unicode 'D'
	include capitalMarks
	
	local dsmooth [SMOOTH * 1.35]
	local bsmooth [SMOOTH * 1.1]
	
	include : VBarLeft SB 0 CAP
	
	include : spiro {
		widths.rhs
		flat [SB - O] CAP [heading RIGHTWARD]
		curl [RIGHTSB - bsmooth] CAP
		archv
		flat RIGHTSB [CAP - dsmooth - [SMOOTHB - SMOOTH]]
		curl RIGHTSB [dsmooth + [SMOOTHA - SMOOTH]]
		arcvh
		flat [RIGHTSB - bsmooth] 0
		curl [SB - O] 0 [heading LEFTWARD]
	}
}
define [PShape top] : glyph-construction {
	local bowlTop [top * 1]
	local bowlBottom [[top - STROKE] * 0.55 - HALFSTROKE]
	
	local turn : mix bowlTop bowlBottom [SMOOTHB / [SMOOTHA + SMOOTHB]]
	local turnRadius : [bowlTop - bowlBottom] * 0.45
	include : spiro {
		widths.rhs
		flat [SB * 1.25 - O] bowlTop [heading RIGHTWARD]
		curl [RIGHTSB - turnRadius - OMIDCOR_S] bowlTop
		archv
		g4 [RIGHTSB - O] turn
		arcvh
		flat [RIGHTSB - turnRadius + OMIDCOR_S] bowlBottom
		curl [SB * 1.25 - O] bowlBottom [heading LEFTWARD]
	}
	include : VBarLeft [SB * 1.25] 0 top
}
define [RShape top] : glyph-construction {
	include : PShape top
	local TURN [XH * 0.1]
	local right [RIGHTSB - O]
	include : create-stroke
		:.start-from [right - HALFSTROKE] 0
		:.heads-to UPWARD
		:.set-width HALFSTROKE HALFSTROKE
		:.curve-to [right - HALFSTROKE] [TURN + 0.2 * [XH - TURN]] MIDDLE [[top - STROKE] * 0.55]
}
create-glyph 'P' : glyph-construction {
	set-width WIDTH
	assign-unicode 'P'
	include capitalMarks
	include : PShape CAP
}
create-glyph 'R' : glyph-construction {
	set-width WIDTH
	assign-unicode 'R'
	include capitalMarks
	include : RShape CAP
}

### C G O zero Q U

create-glyph 'C' : glyph-construction {
	set-width WIDTH
	assign-unicode 'C'
	include capitalMarks
	
	include : spiro {
		widths.lhs
		g4 RIGHTSB [CAP - HOOK]
		hookstart CAPO
		flat SB [CAP - SMOOTHA]
		curl SB SMOOTHB
		hookend O
		g4 RIGHTSB HOOK
	}
}
define [GShape top sma smb] : glyph-construction {
	include : spiro {
		widths.lhs
		g4   RIGHTSB [top - HOOK]
		hookstart [top - O]
		flat SB [top - sma]
		curl SB SMOOTHB
		arcvh
		g4   [MIDDLE + OMIDCOR_S] O
		archv
		flat RIGHTSB sma
		curl RIGHTSB [top / 2 + STROKE / 2] [heading UPWARD]
	}
	include : create-stroke
		:.start-from  MIDDLE [top / 2 + STROKE / 2]
		:.set-width   0 STROKE 
		:.line-to     RIGHTSB [top / 2 + STROKE / 2]
		:.heads-to    RIGHTWARD
}
create-glyph 'G' : glyph-construction {
	set-width WIDTH
	assign-unicode 'G'
	include capitalMarks
	
	include : GShape CAP SMOOTHA SMOOTHB
}

create-glyph 'O' : glyph-construction {
	set-width WIDTH
	assign-unicode 'O'
	include capitalMarks
	
	include : smallo CAP 0 SB RIGHTSB nothing SMOOTHA SMOOTHB
}

create-glyph 'Q' : glyph-construction {
	set-width WIDTH
	assign-unicode 'Q'
	
	include    glyphs.O AS_BASE
	
	start-from MIDDLE 0
	line-to    [MIDDLE + STROKE / 2] [-CAP * 0.2]
	line-to    [MIDDLE + STROKE / 2 + STROKE] [-CAP * 0.2]
	line-to    [MIDDLE + STROKE] 0
	line-to    [MIDDLE + STROKE * [1 - 0.5 / 3]] [STROKE * 0.5]
	reverse-last
}
define [UShape top bottom stroke] : glyph-construction {
	include : spiro {
		widths.lhs
		flat SB top [heading DOWNWARD]
		curl SB [bottom + SMOOTHB]
		arcvh
		g4   [MIDDLE + OMIDCOR_S] [bottom + O]
		archv
		flat RIGHTSB [bottom + SMOOTHA]
		curl RIGHTSB top [heading UPWARD]
	}
}
create-glyph 'U' : glyph-construction {
	set-width WIDTH
	assign-unicode 'U'
	include capitalMarks
	include : UShape CAP 0
}


### F E H L I T Z
define [FShape top y] : glyph-construction {
	include : VBarLeft [SB * 1.5] 0 [fallback top CAP]
	include : HBarTop [SB * 1.5 - O] RIGHTSB [fallback top CAP]
	include : HBar [SB * 1.5 - O] [RIGHTSB - HALFSTROKE] [[fallback top CAP] * [fallback y 0.54]]
}
create-glyph 'F' : glyph-construction {
	set-width WIDTH
	assign-unicode 'F'
	include capitalMarks
	include : FShape
}
define [EShape top] : glyph-construction {
	include : FShape top
	include : HBarBottom [SB * 1.5 - O] RIGHTSB 0
}
create-glyph 'E' : glyph-construction {
	set-width WIDTH
	assign-unicode 'E'
	include capitalMarks
	include : EShape CAP
}
define [HShape top] : glyph-construction {
	include : VBarLeft SB 0 top
	include : VBarRight RIGHTSB 0 top
	include : HBar [SB - O] [RIGHTSB + O] [top / 2]
}
create-glyph 'H' : glyph-construction {
	set-width WIDTH
	assign-unicode 'H'
	include capitalMarks
	include : HShape CAP
}
define [LShape top] : glyph-construction {
	include : create-stroke
		:.start-from [SB * 1.5] top
		:.set-width STROKE 0
		:.heads-to DOWNWARD
		:.line-to [SB * 1.5] 0
		:.heads-to DOWNWARD
	include : create-stroke
		:.start-from [SB * 1.5 - O] 0
		:.set-width STROKE 0
		:.heads-to RIGHTWARD
		:.line-to RIGHTSB 0
		:.heads-to RIGHTWARD
}
create-glyph 'L' : glyph-construction {
	set-width WIDTH
	assign-unicode 'L'
	include capitalMarks
	include : LShape CAP
}

create-glyph 'I.straight' : glyph-construction {
	dont-export
	include capitalMarks
	include : create-stroke
		:.start-from MIDDLE 0
		:.heads-to UPWARD
		:.set-width HALFSTROKE HALFSTROKE
		:.line-to MIDDLE CAP
		:.heads-to UPWARD
}
define [ISerifShape top] : glyph-construction {
	# Top serif
	include : create-stroke
		:.start-from [MIDDLE - WIDTH * 0.26 - STROKE * globalTransform.yx] top
		:.set-width 0 STROKE
		:.line-to [MIDDLE + WIDTH * 0.26 - STROKE * globalTransform.yx] top
	# Bar
	include : VBar MIDDLE 0 top
	# Bottom serif
	include : create-stroke
		:.start-from [MIDDLE - WIDTH * 0.26 + STROKE * globalTransform.yx] 0
		:.set-width STROKE 0
		:.line-to [MIDDLE + WIDTH * 0.26 + STROKE * globalTransform.yx] 0
}
create-glyph 'I.serifed' : glyph-construction {
	dont-export
	include glyphs.'I.straight' AS_BASE
	include : ISerifShape CAP
}
select-variant 'I' 'I' 'serifed'

create-glyph 'T' : glyph-construction {
	set-width WIDTH
	assign-unicode 'T'
	include capitalMarks
	
	include : create-stroke :.start-from MIDDLE 0 :.heads-to UPWARD :.set-width HALFSTROKE HALFSTROKE
		:.line-to MIDDLE CAP :.heads-to UPWARD
	include : create-stroke :.start-from SB CAP :.heads-to RIGHTWARD :.set-width 0 STROKE
		:.line-to RIGHTSB CAP :.heads-to RIGHTWARD
}

create-glyph 'Z' : glyph-construction {
	set-width WIDTH
	assign-unicode 'Z'
	include capitalMarks
	
	local cor 1.15
	
	include : create-stroke :.start-from SB CAP :.heads-to RIGHTWARD :.set-width 0 STROKE
		:.line-to RIGHTSB CAP :.heads-to RIGHTWARD
	include : create-stroke :.start-from SB 0 :.heads-to RIGHTWARD :.set-width STROKE 0
		:.line-to RIGHTSB 0 :.heads-to RIGHTWARD
	
	start-from SB STROKE
	line-to [SB + STROKE * cor] STROKE
	line-to RIGHTSB [CAP - STROKE]
	line-to [RIGHTSB - STROKE * cor] [CAP - STROKE]
	reverse-last
}

### J

create-glyph 'J.straight' : glyph-construction {
	set-width WIDTH
	dont-export
	include capitalMarks
	set-anchor 'overlay' BASE [RIGHTSB - STROKE - JBALANCE] [CAP * BARPOS]
	
	local slope [STROKE * 0.00092] 
	local expand 0.35
	local coexpand [[1 - expand] / 2]
	local kappa KAPPA_HOOK
	local smooth : HOOK + 0.75 * STROKE
	
	local hookx [0.5 * SB + OXHOOK]
	
	include : spiro {
		widths.rhs
		flat [RIGHTSB - JBALANCE] CAP [heading DOWNWARD]
		curl [RIGHTSB - JBALANCE] smooth
		hookend O
		g4 hookx HOOK
	}
}
create-glyph 'J.shorthook' : glyph-construction {
	set-width WIDTH
	dont-export
	include capitalMarks
	
	local slope [STROKE * 0.00092]
	local expand 0.35
	local coexpand [[1 - expand] / 2]
	local kappa KAPPA_HOOK
	local smooth : HOOK + 0.75 * STROKE
	
	local hookx [0.5 * SB + OXHOOK]

	include : create-stroke
		:.start-from [RIGHTSB - JBALANCE] CAP :.set-width 0 STROKE :.heads-to DOWNWARD
		:.line-to    [RIGHTSB - JBALANCE] smooth
		:.arc-vh-to  [mix SB RIGHTSB 0.3] O :.heads-to LEFTWARD
}
create-glyph 'J.serifed' : glyph-construction {
	set-width WIDTH
	dont-export
	include glyphs.'J.straight' AS_BASE
	include : leftwardTopSerif [RIGHTSB - HALFSTROKE - JBALANCE] CAP LONGJUT
}
select-variant 'J' 'J' 'serifed'

### M N
define [NShape top left right coward diagcoward] : glyph-construction {
	local topstroke : adviceBlackness [fallback diagcoward 4]
	local stroke : adviceBlackness [fallback coward 1]
	local halftopstroke : topstroke / 2
	
	include : create-stroke 
		:.start-from left 0 :.heads-to UPWARD :.set-width 0 stroke
		:.line-to left [top * 0.4] :.heads-to UPWARD
		:.line-to left top :.heads-to UPWARD :.set-width 0 topstroke
	include : create-stroke 
		:.start-from right 0 :.heads-to UPWARD :.set-width topstroke 0
		:.line-to right [top * 0.6] :.heads-to UPWARD :.set-width stroke 0
		:.line-to right top :.heads-to UPWARD
	include : create-stroke
		:.start-from [left + halftopstroke] top :.heads-to DOWNWARD :.set-width topstroke 0 
		:.line-to [right - topstroke - halftopstroke] 0 :.heads-to DOWNWARD
}
create-glyph 'N' : glyph-construction {
	set-width WIDTH
	assign-unicode 'N'
	include capitalMarks
	include : NShape CAP SB RIGHTSB
}
define [MShape top] : glyph-construction {
	local topstroke : adviceBlackness 5
	local halftopstroke : topstroke / 2
	
	include : create-stroke 
		:.start-from SB 0 :.heads-to UPWARD :.set-width 0 STROKE 
		:.line-to SB [top * 0.2] :.heads-to UPWARD
		:.line-to SB top :.heads-to UPWARD :.set-width 0 topstroke
	include : create-stroke 
		:.start-from RIGHTSB 0 :.heads-to UPWARD :.set-width STROKE 0
		:.line-to RIGHTSB [top * 0.2] :.heads-to UPWARD
		:.line-to RIGHTSB top :.heads-to UPWARD :.set-width topstroke 0
	include : create-stroke
		:.start-from [SB + halftopstroke] top :.heads-to DOWNWARD :.set-width topstroke 0 
		:.line-to [MIDDLE - halftopstroke] [top * 0.3] :.heads-to DOWNWARD
	include : create-stroke
		:.start-from [MIDDLE + halftopstroke] [top * 0.3] :.heads-to UPWARD :.set-width topstroke 0 
		:.line-to [RIGHTSB - halftopstroke] top :.heads-to UPWARD
}
create-glyph 'M' : glyph-construction {
	set-width WIDTH
	assign-unicode 'M'
	include capitalMarks
	include : MShape CAP
}

### S
create-glyph 'S' : glyph-construction {
	set-width WIDTH
	assign-unicode 'S'
	include capitalMarks
	include : spiro {
		widths.lhs
		g4   RIGHTSB [CAP - HOOK]
		hookstart CAPO
		g4   SB [CAP - SMOOTHA]
		alsothru 0.5 0.5 [widths HALFSTROKE HALFSTROKE]
		g4   RIGHTSB SMOOTHA [widths 0 STROKE]
		hookend O
		g4   SB HOOK
	}
#	include : sHookUpper CAP SMOOTHA HOOK
#	include : sHookLower 0 SMOOTHA HOOK
#	include : sStrand [CAP - SMOOTHA] SMOOTHA
}