###### EXTENDED LATIN CHARACTERS
###### DECORATED

# Slashed
create-glyph 'Oslash' : glyph-construction
	assign-unicode 0xD8
	
	local fine : 0.5 * OVERLAYSTROKE
	
	include glyphs.O AS_BASE
	include : create-stroke
		:.start-from (SB + O + fine) [mix CAP 0 1.05]
		:.set-width  fine fine
		:.line-to    (RIGHTSB - O - fine) [mix 0 CAP 1.05]

create-glyph 'oslash' : glyph-construction
	assign-unicode 0xF8
	
	local fine : 0.5 * OVERLAYSTROKE
	
	include glyphs.o AS_BASE
	include : create-stroke
		:.start-from (SB + O + fine) [mix XH 0 1.05]
		:.set-width  fine fine
		:.line-to    (RIGHTSB - O - fine) [mix 0 XH 1.05]

create-glyph 'Lslash' : glyph-construction
	assign-unicode 0x141
	include glyphs.L AS_BASE
	local fine : 0.5 * OVERLAYSTROKE
	local middle : mix STROKE CAP 0.5
	include : create-stroke
		:.start-from [mix 0 SB 0.5] (middle - LONGJUT * 0.4)
		:.set-width  fine fine
		:.line-to    (1.6 * LONGJUT + [mix 0 SB 0.5]) (middle + LONGJUT * 0.4)

define [FlatSlashShape middlex middle fine] : glyph-construction
	include : create-stroke
		:.start-from (middlex - LONGJUT * 0.8) (middle - LONGJUT * 0.4)
		:.set-width  fine fine
		:.line-to    (middlex + LONGJUT * 0.8) (middle + LONGJUT * 0.4)

create-glyph 'lslash' : glyph-construction
	assign-unicode 0x142
	include glyphs.l AS_BASE
	include : FlatSlashShape [tp [Upright] glyphs.l.anchors.above].x [mix 0 CAP BARPOS] (0.5 * OVERLAYSTROKE)

create-glyph 'lambdaslash' : glyph-construction
	assign-unicode 0x19B
	include glyphs.lambda AS_BASE
	local fine : 0.5 * [adviceBlackness 5]
	local middle : mix 0 CAP 0.7
	local middlex [tp [Upright] glyphs.l.anchors.above].x
	include : create-stroke
		:.start-from (middlex - LONGJUT * 0.8) (middle - LONGJUT * 0.4)
		:.set-width  fine fine
		:.line-to    (middlex + LONGJUT * 0.8) (middle + LONGJUT * 0.4)

create-glyph 'longsslash' : glyph-construction
	assign-unicode 0x1E9C
	include glyphs.longs AS_BASE
	include : FlatSlashShape [tp [Upright] glyphs.l.anchors.above].x [mix 0 CAP BARPOS] (0.5 * OVERLAYSTROKE)

# Barred
create-glyph 'Gbar' : glyph-construction
	assign-unicode 0x1E4
	include glyphs.G AS_BASE
	include : HOverlayBar [mix SB RIGHTSB 0.5] [mix RIGHTSB WIDTH 0.7] [mix 0 CAPMIDDLE 0.55]

create-glyph 'gbar' : glyph-construction
	assign-unicode 0x1E5
	include glyphs.g AS_BASE
	include : HBar [mix 0 SB 0.5] [mix SB RIGHTSB 0.5] glyphs.g.anchors.overlay.y OVERLAYSTROKE

create-glyph 'Hbar' : glyph-construction
	assign-unicode 0x126
	
	include glyphs.H AS_BASE
	include : HOverlayBar [mix SB 0 0.7] [mix RIGHTSB WIDTH 0.7] [mix CAPMIDDLE CAP 0.45]

create-glyph 'hbar' : glyph-construction
	assign-unicode 0x127
	
	include glyphs.h AS_BASE
	include : HOverlayBar [mix SB 0 0.7] [mix SB RIGHTSB 0.5] [mix XH CAP 0.45]

create-glyph 'Bbar' : glyph-construction
	assign-unicode 0x243
	
	include glyphs.B AS_BASE
	include : HOverlayBar [mix SB 0 0.7] [mix SB RIGHTSB 0.5] [mix 0 CAP 0.3]

create-glyph 'bbar' : glyph-construction
	assign-unicode 0x180
	
	include glyphs.b AS_BASE
	include : HOverlayBar [mix SB 0 0.7] [mix SB RIGHTSB 0.5] [mix XH CAP 0.45]

create-glyph 'Tbar' : glyph-construction
	assign-unicode 0x166
	
	include glyphs.T AS_BASE
	include : HOverlayBar [mix MIDDLE SB 0.8] [mix MIDDLE RIGHTSB 0.8] [mix 0 CAP 0.45]

create-glyph 'tbar' : glyph-construction
	assign-unicode 0x167
	
	include glyphs.t AS_BASE
	include : HOverlayBar ([mix MIDDLE SB 0.7] - TBALANCE * 0.5) ([mix MIDDLE RIGHTSB 0.7] - TBALANCE * 0.5) [mix 0 XH 0.6]

create-glyph 'Lbar' : glyph-construction
	assign-unicode 0x23D
	include glyphs.L AS_BASE
	include : HOverlayBar (SB * 0.3) [mix (SB + STROKE) (RIGHTSB - STROKE) 0.55] (CAP * BARPOS)

create-glyph 'Cbar' : glyph-construction
	assign-unicode 0xA792
	include glyphs.C AS_BASE
	include : HOverlayBar (SB * 0.3) [mix (SB + STROKE) (RIGHTSB - STROKE) 0.55] (CAP * BARPOS)

create-glyph 'cbar' : glyph-construction
	assign-unicode 0xA793
	include glyphs.c AS_BASE
	include : HOverlayBar (SB * 0.3) [mix (SB + STROKE) (RIGHTSB - STROKE) 0.55] (XH * BARPOS)

# Dotted
create-glyph 'Ldot' : glyph-construction
	assign-unicode 0x13F
	include glyphs.L AS_BASE
	include : list
		Ring ([mix STROKE CAP 0.5] + DOTRADIUS) ([mix STROKE CAP 0.5] - DOTRADIUS) ([mix SB RIGHTSB 0.65] - DOTRADIUS) ([mix SB RIGHTSB 0.65] + DOTRADIUS)

create-glyph 'ldot' : glyph-construction
	assign-unicode 0x140
	include glyphs.l AS_BASE
	apply-transform : Translate (-DOTRADIUS / 2) 0
	include : list
		Ring ([mix STROKE CAP 0.5] + DOTRADIUS) ([mix STROKE CAP 0.5] - DOTRADIUS) ([mix SB RIGHTSB 1] - DOTSIZE) [mix SB RIGHTSB 1]

create-glyph 'Obar' : glyph-construction
	assign-unicode 0x19F
	include glyphs.O AS_BASE
	include : HOverlayBar (SB - O) (RIGHTSB + O) (CAP * 0.5)

create-glyph 'obar' : glyph-construction
	assign-unicode 0x275
	include glyphs.o AS_BASE
	include : HOverlayBar (SB - O) (RIGHTSB + O) (XH * 0.5)

create-glyph 'glottalstopbar' : glyph-construction
	assign-unicode 0x2A1
	include glyphs.glottalstop AS_BASE
	include : HOverlayBar (MIDDLE - LONGJUT * 0.6) (MIDDLE + LONGJUT * 0.6) (CAP * 0.25)

create-glyph 'revglottalstopbar' : glyph-construction
	assign-unicode 0x2A2
	include glyphs.revglottalstop AS_BASE
	include : HOverlayBar (MIDDLE - LONGJUT * 0.6) (MIDDLE + LONGJUT * 0.6) (CAP * 0.25)

create-glyph 'eshbar' : glyph-construction
	assign-unicode 0x284
	include glyphs.esh AS_BASE
	include : HOverlayBar (MIDDLE - LONGJUT * 0.6) (MIDDLE + LONGJUT * 0.6) (XH * 0.25)

# bdot and ddot 's dots are closer to the glyph
create-glyph 'bdot' : glyph-construction
	include glyphs.dotAbove
	apply-transform : Translate (WIDTH + HALFSTROKE) 0
	include glyphs.b AS_BASE
	assign-unicode 0x1E03

create-glyph 'ddot' : glyph-construction
	include glyphs.dotAbove
	apply-transform : Translate (WIDTH - HALFSTROKE) 0
	include glyphs.d AS_BASE
	assign-unicode 0x1E0B

# Legged
create-glyph 'rlongleg' : glyph-construction
	assign-unicode 0x27C
	include pMarks
	include glyphs.r
	include : VBarLeft (SB + RBALANCE) DESCENDER 0

create-glyph 'turnmleg' : glyph-construction
	assign-unicode 0x270
	include pMarks
	include glyphs.m
	include : FlipAround MIDDLE (XH / 2)
	include : VBarRight (RIGHTSB - O) DESCENDER 0 MVERTSTROKE

# Hookleft
create-glyph 'Bhookleft' : glyph-construction
	assign-unicode 0x181
	include glyphs.B AS_BASE
	include : LeftHook SB CAP

create-glyph 'Dhookleft' : glyph-construction
	assign-unicode 0x18A
	include glyphs.D AS_BASE
	include : LeftHook SB CAP

create-glyph 'Phookleft' : glyph-construction
	assign-unicode 0x1A4
	include glyphs.P AS_BASE
	include : LeftHook (SB * 1.25) CAP

create-glyph 'Thookleft' : glyph-construction
	assign-unicode 0x1AC
	include capitalMarks
	include : LeftHook (SB + [adviceBlackness 4.25] * 1.5) CAP MIDDLE
	include : HBarTop MIDDLE RIGHTSB CAP
	include : VBar MIDDLE 0 CAP

# Hooktop
define [HooktopLeftBar stroke bottom] : glyph-construction
	include : spiro
		widths.lhs [fallback stroke STROKE]
		g4 RIGHTSB (CAP - HOOK)
		hookstart CAPO
		flat SB (CAP - SMALLSMOOTHA)
		curl SB [fallback bottom 0] [heading DOWNWARD]

create-glyph 'hhooktop' : glyph-construction
	assign-unicode 0x266
	include bMarks
	include : nShoulder (SB + STROKE * CORRECTION_HX) MIDDLE RIGHTSB (STROKE * 0.4)
	include : HooktopLeftBar

create-glyph 'bhooktop' : glyph-construction
	assign-unicode 0x253
	include bMarks
	include : oLeft
	include : HooktopLeftBar

create-glyph 'phooktop' : glyph-construction
	assign-unicode 0x1A5
	include ifMarks
	include : oLeft
	include : HooktopLeftBar nothing DESCENDER

create-glyph 'Khooktop' : glyph-construction
	assign-unicode 0x198
	include capitalMarks
	include : KShape
	include : VBarLeft SB 0 (CAP - HOOK - HALFSTROKE)
	include : VerticalHook (SB + HALFSTROKE * CORRECTION_HX) (CAP - HOOK - HALFSTROKE) HOOKX (-HOOK)

create-glyph 'khooktop' : glyph-construction
	assign-unicode 0x199
	include bMarks
	include : SmallKShape
	include : VBarLeft SB 0 (CAP - HOOK - HALFSTROKE)
	include : VerticalHook (SB + HALFSTROKE * CORRECTION_HX) (CAP - HOOK - HALFSTROKE) HOOKX (-HOOK)

create-glyph 'scriptghooktop' : glyph-construction
	assign-unicode 0x260
	include glyphs.scriptg AS_BASE
	include : VerticalHook (RIGHTSB - HALFSTROKE * CORRECTION_HX) XH HOOKX (-HOOK)

create-glyph 'thooktop' : glyph-construction
	assign-unicode 0x1AD
	include eMarks
	include : SmallTShape (XH - HALFSTROKE) 0
	include : VerticalHook (MIDDLE - TBALANCE) XH HOOKX (-HOOK)

create-glyph 'dhooktop' : glyph-construction
	assign-unicode 0x257
	include bMarks
	include glyphs.'a.italic'
	include : VerticalHook (RIGHTSB - HALFSTROKE * CORRECTION_HX) XH HOOKX (-HOOK)

create-glyph 'qhooktop' : glyph-construction
	assign-unicode 0x2A0
	include pMarks
	include glyphs.'a.italic'
	include : VerticalHook (RIGHTSB - HALFSTROKE * CORRECTION_HX) XH HOOKX (-HOOK)
	include : VBarRight RIGHTSB DESCENDER 0

create-glyph 'Chooktop' : glyph-construction
	assign-unicode 0x187
	include glyphs.C AS_BASE
	include : VBarRight RIGHTSB (CAP - HOOK) CAP
	include : VerticalHook (RIGHTSB - HALFSTROKE * CORRECTION_HX) CAP HOOKX (-HOOK)

create-glyph 'chooktop' : glyph-construction
	assign-unicode 0x188
	include glyphs.c AS_BASE
	include : VBarRight RIGHTSB (XH - HOOK) XH
	include : VerticalHook (RIGHTSB - HALFSTROKE * CORRECTION_HX) XH HOOKX (-HOOK)

create-glyph 'Ghooktop' : glyph-construction
	assign-unicode 0x193
	include glyphs.G AS_BASE
	include : VBarRight RIGHTSB (CAP - HOOK) CAP
	include : VerticalHook (RIGHTSB - HALFSTROKE * CORRECTION_HX) CAP HOOKX (-HOOK)

create-glyph 'smcpGhooktop' : glyph-construction
	assign-unicode 0x29B
	include glyphs.smcpG AS_BASE
	include : VBarRight RIGHTSB (XH - HOOK) XH
	include : VerticalHook (RIGHTSB - HALFSTROKE * CORRECTION_HX) XH HOOKX (-HOOK)

define [VHooktopShape top] : glyph-construction
	local TURN (top * 0.9)
	local m : MIDDLE - HOOKX / 10
	include : create-stroke
		:.start-from SB top
		:.heads-to   DOWNWARD
		:.set-width  STROKE 0
		:.line-to    SB TURN
		:.heads-to   DOWNWARD
		:.curve-to   SB ((1 - 0.27) * TURN) (m - STROKE / 2) 0
		:.set-width  (STROKE * 0.9) 0
	
	local x1 : RIGHTSB - HOOKX / 3
	local x2 : mix m RIGHTSB 0.52
	local y1 : TURN - HOOK
	local y2 : mix 0 top 0.3
	include : create-stroke
		:.start-from (RIGHTSB + HOOKX / 4) (top - STROKE)
		:.set-width  0 STROKE
		:.curve-to   [linreg y1 x1 y2 x2 (top - STROKE)] (top - STROKE) x1 y1
		:.curve-to   x2 y2 (m + STROKE / 2) 0
		:.set-width  0 (STROKE * 0.9)
	
	#bottom cap
	start-from (m + STROKE / 2) 0
	line-to (m - STROKE / 2) 0
	line-to m (STROKE * 0.8)


create-glyph 'vhooktop' : glyph-construction
	assign-unicode 0x2C71
	include eMarks
	include : VHooktopShape XH

create-glyph 'whooktop' : glyph-construction
	assign-unicode 0x2C73
	include eMarks
	
	local TURN (XH * 0.75)
	local turn2 (XH * 0.59)
	local wheight (XH * 0.6)
	local bottomStroke : adviceBlackness 5.2
	local m1 : WIDTH * 0.325
	local m2 : WIDTH * 0.675
	
	include : create-stroke
		:.start-from SB XH
		:.heads-to DOWNWARD
		:.set-width STROKE 0
		:.line-to SB TURN
		:.heads-to DOWNWARD
		:.curve-to SB ((1 - 0.27) * TURN) (m1 - bottomStroke / 2) 0
		:.set-width bottomStroke 0
	
	include : create-stroke
		:.start-from RIGHTSB (XH - HOOK)
		:.heads-to DOWNWARD
		:.set-width 0 STROKE
		:.curve-to RIGHTSB ((1 - 0.27) * TURN) (m2 + bottomStroke / 2) 0
		:.set-width 0 bottomStroke
		
	include : create-stroke
		:.start-from (MIDDLE + bottomStroke / 2 * CORRECTION_HX) wheight
		:.heads-to DOWNWARD
		:.set-width 0 bottomStroke
		:.line-to (MIDDLE + bottomStroke / 2 * CORRECTION_HX) turn2
		:.heads-to DOWNWARD
		:.curve-to (MIDDLE + bottomStroke / 2 * CORRECTION_HX) ((1 - 0.1) * turn2) (m1 + bottomStroke / 2) 0
		:.set-width 0 bottomStroke
	
	include : create-stroke
		:.start-from (MIDDLE - bottomStroke / 2 * CORRECTION_HX) wheight
		:.heads-to DOWNWARD
		:.set-width bottomStroke 0
		:.line-to (MIDDLE - bottomStroke / 2 * CORRECTION_HX) turn2
		:.heads-to DOWNWARD
		:.curve-to (MIDDLE - bottomStroke / 2 * CORRECTION_HX) ((1 - 0.1) * turn2) (m2 - bottomStroke / 2) 0
		:.set-width bottomStroke 0

	#bottom cap
	start-from (m1 + bottomStroke / 2) 0
	line-to (m1 - bottomStroke / 2) 0
	line-to m1 bottomStroke
	
	start-from (m2 + bottomStroke / 2) 0
	line-to (m2 - bottomStroke / 2) 0
	line-to m2 bottomStroke
	include : VerticalHook (RIGHTSB - HALFSTROKE * CORRECTION_HX) (XH - HOOK) (HOOKX * 0.65) (-HOOK + HALFSTROKE)

create-glyph 'Yhooktop' : glyph-construction
	assign-unicode 0x1B3
	include capitalMarks
	
	local cross (CAP * 0.4)
	local TURN [mix cross CAP 0.9]
	include : halfXStrand SB CAP MIDDLE cross 0.1 0.4 0.28
	
	local x1 : RIGHTSB - HOOKX / 3
	local x2 : mix MIDDLE RIGHTSB 0.52
	local y1 : TURN - HOOK
	local y2 : mix cross CAP 0.3
	include : create-stroke
		:.start-from (RIGHTSB + HOOKX / 4) (CAP - STROKE)
		:.set-width  0 STROKE
		:.curve-to   [linreg y1 x1 y2 x2 (CAP - STROKE)] (CAP - STROKE) x1 y1
		:.curve-to   x2 y2 (MIDDLE + STROKE / 2 * CORRECTION_HX) cross
	
	include : VBar MIDDLE 0 (cross + HALFSTROKE)

create-glyph 'yhooktop' : glyph-construction
	assign-unicode 0x1B4
	include pMarks
	
	local xbottom : mix SB RIGHTSB 0.28
	local turnp : XH / (XH - DESCENDER)
	local xb : mix SB RIGHTSB 0.49
	local yb : mix 0 XH (0.05 * turnp)
	
	# include : xStrand xbottom DESCENDER RIGHTSB XH 0.1 0.6 0.14
	include : spiro
		widths.center
		flat (xbottom + HALFSTROKE * CORRECTION_HX) DESCENDER [heading UPWARD]
		curl (xbottom + HALFSTROKE * CORRECTION_HX) (DESCENDER + 1) [heading UPWARD]
		alsothruthem {{0 0.05} {0.1 0.19} {0.2125 0.33} {0.44 0.6} {0.93 0.98}} important
		g2   (RIGHTSB - HOOKX / 6) [mix DESCENDER (XH - HALFSTROKE) 0.98]
		flat (RIGHTSB + HOOKX / 4 - 1) (XH - HALFSTROKE)
		curl (RIGHTSB + HOOKX / 4) (XH - HALFSTROKE)
	include : halfXStrand SB XH xb yb (0.1 * turnp) 0.4 (0.14 * turnp)

# Left-tail
create-glyph 'fltail' : glyph-construction
	assign-unicode 0x192
	include glyphs.'longs.italic' AS_BASE
	include : fBar

create-glyph 'Fltail' : glyph-construction
	assign-unicode 0x191
	include glyphs.F AS_BASE
	include : VerticalHook (SB * 1.5 + HALFSTROKE * CORRECTION_HX) 0 (-HOOKX) HOOK

create-glyph 'nltail' : glyph-construction
	assign-unicode 0x272
	include glyphs.n AS_BASE
	include : VerticalHook (SB + HALFSTROKE * CORRECTION_HX) 0 (-HOOKX) HOOK

create-glyph 'tltail' : glyph-construction
	assign-unicode 0x1AB
	include ifMarks
	include glyphs.t
	local attach : utp currentGlyph.gizmo glyphs.t.anchors.bottomright
	include : VBarRight attach.x 0 attach.y
	include : VerticalHook (attach.x - HALFSTROKE * CORRECTION_HX) 0 (-HOOKX) HOOK

create-glyph 'Nltail' : glyph-construction
	assign-unicode 0x19D
	include glyphs.N AS_BASE
	include : VerticalHook (SB + HALFSTROKE * CORRECTION_HX) 0 (-HOOKX) HOOK

create-glyph 'mltail' : glyph-construction
	assign-unicode 0x271
	include pMarks
	include glyphs.m
	include : VerticalHook (RIGHTSB - O - MVERTSTROKE / 2 * CORRECTION_HX) 0 (-HOOKX) HOOK MVERTSTROKE

create-glyph 'hhooktopltail' : glyph-construction
	assign-unicode 0x267
	include pMarks
	include glyphs.hhooktop
	include : VerticalHook (RIGHTSB - HALFSTROKE * CORRECTION_HX) 0 (-HOOKX) HOOK

create-glyph 'hltail' : glyph-construction
	assign-unicode 0xA727
	include pMarks
	include glyphs.h
	include : VerticalHook (RIGHTSB - HALFSTROKE * CORRECTION_HX) 0 (-HOOKX) HOOK

create-glyph 'hookturnh' : glyph-construction
	assign-unicode 0x2AE
	include : nShoulder (SB + STROKE * CORRECTION_HX) [mix SB RIGHTSB 0.5] RIGHTSB (STROKE * 0.4) XH (HOOK + HALFSTROKE + 1) SMOOTHA SMOOTHB
	include : VBarLeft SB 0 CAP
	include : create-glyph : glyph-construction
		include glyphs.rtailBR
		apply-transform : Upright
		apply-transform : Translate RIGHTSB (HOOK + HALFSTROKE)
		apply-transform : Italify
	include : FlipAround MIDDLE (XH / 2)
	include pMarks

# Right-tail
create-glyph 'srtail' : glyph-construction
	assign-unicode 0x282
	include glyphs.rtailBR
	apply-transform : Upright
	apply-transform : Translate (SB + STROKE * CORRECTION_HX) 0
	apply-transform : Italify
	include pMarks
	include glyphs.s
	include : VBarLeft SB 0 SHOOK

create-glyph 'lrtail' : glyph-construction
	assign-unicode 0x26D
	include glyphs.rtailBR
	apply-transform : Translate (MIDDLE + HALFSTROKE * CORRECTION_HX) 0
	include glyphs.'l.hooky'
	include ifMarks

create-glyph 'rrtail' : glyph-construction
	assign-unicode 0x27D
	include pMarks
	include glyphs.r
	include : create-glyph : glyph-construction
		include glyphs.rtailBR
		apply-transform : Upright
		apply-transform : Translate (SB + RBALANCE + STROKE * CORRECTION_HX) 0
		apply-transform : Italify

create-glyph 'turnrrtail' : glyph-construction
	assign-unicode 0x27B
	include pMarks
	include glyphs.r
	include : FlipAround MIDDLE (XH / 2)
	include : create-glyph : glyph-construction
		include glyphs.rtailBR
		apply-transform : Upright
		apply-transform : Translate (RIGHTSB - RBALANCE) 0
		apply-transform : Italify

create-glyph 'trtail' : glyph-construction
	assign-unicode 0x288
	include ifMarks
	include : SmallTShape CAP DESCENDER

create-glyph 'ezhtail' : glyph-construction
	assign-unicode 0x1BA
	local b : mix DESCENDER XH 0.25
	include : EzhShape XH b nothing true
	local y : mix [mix b XH 0.6] b 0.5
	include : create-stroke
		:.start-from RIGHTSB y
		:.heads-to   DOWNWARD
		:.set-width  0 STROKE
		:.arc-vh-to  [mix SB RIGHTSB 0.45] b
		:.line-to    [mix SB RIGHTSB 0.4] b
		:.arc-hv-to  (SB + STROKE * CORRECTION_HX) [mix (DESCENDER + STROKE) b 0.5]
		:.arc-vh-to  [mix SB RIGHTSB 0.4] (DESCENDER + STROKE)
		:.line-to    RIGHTSB (DESCENDER + STROKE)

create-glyph 'Qrtail' : glyph-construction
	assign-unicode 0x24A
	include capitalMarks
	include : oRight CAP
	include : VBarRight RIGHTSB 0 CAP
	include : create-glyph : glyph-construction
		include glyphs.rtailBR
		apply-transform : Upright
		apply-transform : Translate RIGHTSB 0
		apply-transform : Italify

create-glyph 'qrtail' : glyph-construction
	assign-unicode 0x24B
	include eMarks
	include : oRight
	include : VBarRight RIGHTSB 0 XH
	include : create-glyph : glyph-construction
		include glyphs.rtailBR
		apply-transform : Upright
		apply-transform : Translate RIGHTSB 0
		apply-transform : Italify

create-glyph 'hookturnhrtail' : glyph-construction
	assign-unicode 0x2AF
	include : nShoulder (SB + STROKE * CORRECTION_HX) [mix SB RIGHTSB 0.5] RIGHTSB (STROKE * 0.4) XH (HOOK + HALFSTROKE + 1) SMOOTHA SMOOTHB
	include : VBarLeft SB 0 (XH - 1)
	include : create-glyph : glyph-construction
		include glyphs.rtailBR
		apply-transform : Upright
		apply-transform : Translate RIGHTSB (HOOK + HALFSTROKE)
		apply-transform : Italify
	include : FlipAround MIDDLE (XH / 2)
	include : create-glyph : glyph-construction
		include glyphs.rtailBR
		apply-transform : Upright
		apply-transform : Translate RIGHTSB 0
		apply-transform : Italify
	include pMarks

# Downtail
create-glyph 'Zdtail' : glyph-construction
	assign-unicode 0x224
	include glyphs.Z AS_BASE
	this.contours.pop
	include : spiro
		widths.lhs
		flat SB 0
		curl (RIGHTSB - HOOKX) 0
		archv
		flat RIGHTSB (-HOOK) [widths 0 STROKE]
		curl RIGHTSB (-HOOK - 1)

create-glyph 'zdtail' : glyph-construction
	assign-unicode 0x225
	include glyphs.z AS_BASE
	this.contours.pop
	include : spiro
		widths.lhs
		flat SB 0 [heading RIGHTWARD]
		curl (RIGHTSB - HOOKX) 0
		archv
		g4 RIGHTSB (-HOOK) [widths.heading 0 STROKE DOWNWARD]

# Swash tail
create-glyph 'Sswash' : glyph-construction
	assign-unicode 0x2C7E
	include ifMarks
	include glyphs.S
	include : spiro
		widths.lhs [adviceBlackness 4.5]
		g4 (SB - OXHOOK) HOOK
		alsothru 0.3 0.6 important
		flat (RIGHTSB - 1) DESCENDER [widths STROKE 0]
		curl RIGHTSB DESCENDER

create-glyph 'sswash' : glyph-construction
	assign-unicode 0x23F
	include pMarks
	include glyphs.s
	include : spiro
		widths.lhs [adviceBlackness 4.5]
		g4 (SB - OXHOOK) SHOOK
		alsothru 0.36 0.6 important
		flat (RIGHTSB - 1) DESCENDER [widths STROKE 0]
		curl RIGHTSB DESCENDER

create-glyph 'Zswash' : glyph-construction
	assign-unicode 0x2C7F
	include ifMarks
	local cor 1.15
	
	include : HBarTop SB RIGHTSB CAP
	
	start-from SB 0
	line-to (SB + STROKE * cor) 0
	line-to RIGHTSB (CAP - STROKE)
	line-to (RIGHTSB - STROKE * cor) (CAP - STROKE)
	reverse-last
	
	include : spiro
		widths.lhs
		g4 SB 0
		alsothru 0.36 0.6 important
		flat (RIGHTSB - 1) DESCENDER
		curl RIGHTSB DESCENDER

create-glyph 'zswash' : glyph-construction
	assign-unicode 0x240
	include pMarks
	
	include : HBarTop SB RIGHTSB XH

	local cor 1.2
	start-from SB 0
	line-to (SB + STROKE * cor) 0
	line-to RIGHTSB (XH - STROKE)
	line-to (RIGHTSB - STROKE * cor) (XH - STROKE)
	reverse-last

	include : spiro
		widths.lhs
		g4 (SB - OXHOOK) 0
		alsothru 0.36 0.6 important
		flat (RIGHTSB - 1) DESCENDER
		curl RIGHTSB DESCENDER

# Descended
create-glyph 'Zdesc' : glyph-construction
	assign-unicode 0x2C6B
	include glyphs.Z AS_BASE
	include : VBarRight [mix SB RIGHTSB 1.05] (HALFSTROKE - LONGJUT) STROKE

create-glyph 'zdesc' : glyph-construction
	assign-unicode 0x2C6C
	include glyphs.z AS_BASE
	include : VBarRight [mix SB RIGHTSB 1.05] (HALFSTROKE - LONGJUT) STROKE

# Curly tail
define [CurlyTail fine rinner m1 bottom _right x2 y2] : begin
	local right : _right - fine * [if (_right > m1) 1 (-1)]
	local mid : mix [mix m1 right 0.5] (right - rinner * [if (_right > m1) 1 (-1)]) 0.4
	return : list
		g4   mid (bottom + fine + O) [widths [if (_right > m1) 0 fine] [if (_right > m1) fine 0]]
		archv 2
		g4   right (bottom + fine + rinner - 0.1)
		g4   right (bottom + fine + rinner + 0.1)
		arcvh 2
		g4   mid (bottom + fine + rinner * 2 - O)
		alsothruthem {{0.25 0.06} {0.52 0.25}} important
		g4   x2 y2

define [HCurlyTail fine wide rinner left m1 _right x1 x2 y2] : glyph-construction
	local right : _right - fine
	local mid : right - rinner + O
	include : create-stroke
		:.start-from left wide
		:.heads-to   RIGHTWARD
		:.set-width  0 wide
		:.line-to    m1 wide
		:.arc-hv-to  right (wide + rinner)
		:.set-width  0 fine
		:.arc-vh-to  mid (wide + rinner * 2 - O)
		:.curve-to   x1 (wide + rinner * 2 - O) x2 y2

create-glyph 'ncurlytail' : glyph-construction
	assign-unicode 0x235
	local fine : adviceBlackness 4
	local rinner : fine * 0.35
	local wide STROKE
	local m1 : RIGHTSB - rinner * 2
	local x2 : mix SB m1 0.5
	local y2 : -fine
	include eMarks
	include : VBarLeft SB 0 XH wide
	include : spiro
		nShoulderKnots (SB + wide * CORRECTION_HX) [mix SB m1 0.5] m1 (STROKE * 0.4) nothing (fine * 2) nothing nothing wide
		CurlyTail fine rinner m1 0 (m1 + rinner * 2 + fine) x2 y2

create-glyph 'dcurlytail' : glyph-construction
	assign-unicode 0x221
	local fine : adviceBlackness 4
	local rinner : fine * 0.35
	local wide STROKE
	local m1 : RIGHTSB - rinner * 2
	local x2 : mix SB m1 0.5
	local y2 : -fine * 1.5
	include eMarks
	include : list
		ORing XO O (SB + O) (m1 - HALFSTROKE) SMALLSMOOTHA SMALLSMOOTHB 0
		ORing (XO - STROKE) (O + STROKE) (SB + STROKE * CORRECTION_HX + O) (m1 - STROKE * CORRECTION_HX) (SMALLSMOOTHA - STROKE) (SMALLSMOOTHB - STROKE) 0
	reverse-last
	include : spiro
		widths.rhs
		flat m1 CAP [heading DOWNWARD]
		curl m1 (fine * 2)
		CurlyTail fine rinner m1 0 (m1 + rinner * 2 + fine) x2 y2

create-glyph 'lcurlytail' : glyph-construction
	assign-unicode 0x234
	include bMarks
	local fine : adviceBlackness 3.5
	local rinner : LONGJUT / 2 - fine / 2
	local m1 : MIDDLE + HALFSTROKE * CORRECTION_HX
	local x2 : mix SB m1 0.25
	local y2 : - fine
	include : leftwardTopSerif MIDDLE CAP LONGJUT
	include : spiro
		widths.rhs
		flat m1 CAP [heading DOWNWARD]
		curl m1 (fine * 2)
		CurlyTail fine rinner m1 0 (m1 + LONGJUT) x2 y2

create-glyph 'jcurlytail' : glyph-construction
	assign-unicode 0x29D
	include pMarks
	set-anchor 'above' BASE (MIDDLE + JBALANCE) XH
	local fine : adviceBlackness 3.5
	local rinner : LONGJUT / 2 - fine / 2
	local m1 : MIDDLE - HALFSTROKE * CORRECTION_HX + JBALANCE
	local x2 : mix RIGHTSB m1 0.25
	local y2 : DESCENDER + O
	include : leftwardTopSerif (MIDDLE + JBALANCE) XH LONGJUT
	include : spiro
		widths.lhs
		flat m1 XH [heading DOWNWARD]
		curl m1 (DESCENDER + fine * 3)
		CurlyTail fine rinner m1 DESCENDER (m1 - LONGJUT) x2 y2
	include glyphs.dotAbove

create-glyph 'eshcurlytail' : glyph-construction
	assign-unicode 0x286
	include ifMarks
	local fine : adviceBlackness 3.5
	local rinner : LONGJUT / 2 - fine / 2
	local m1 : MIDDLE - HALFSTROKE * CORRECTION_HX - FBALANCE
	local x2 : mix RIGHTSB m1 0.25
	local y2 : DESCENDER + O
	include glyphs.'longs.upright'
	include : spiro
		widths.lhs
		flat m1 XH [heading DOWNWARD]
		curl m1 (DESCENDER + fine * 3)
		CurlyTail fine rinner m1 DESCENDER (m1 - LONGJUT) x2 y2

create-glyph 'tcurlytail' : glyph-construction
	assign-unicode 0x236
	include bMarks
	local fine : adviceBlackness 3.5
	local rinner : LONGJUT / 2 - fine / 2
	local m1 : MIDDLE - TBALANCE - HALFSTROKE
	local x2 : mix SB m1 0.4
	local y2 : -fine
	include : HBarTop (m1 + HALFSTROKE - LONGJUT + TBALANCE2) (m1 + HALFSTROKE + LONGJUT + TBALANCE2) XH
	include : spiro
		widths.lhs
		flat m1 CAP [heading DOWNWARD]
		curl m1 (fine * 2)
		CurlyTail fine rinner (m1 + STROKE * CORRECTION_HX) 0 (m1 + STROKE * CORRECTION_HX + LONGJUT) x2 y2

create-glyph 'ccurlytail' : glyph-construction
	assign-unicode 0x255
	include eMarks
	local fine : adviceBlackness 3
	local rinner : ((XH * 0.45) - fine * 1.5) / 2
	local m1 : SB + O + STROKE * CORRECTION_HX
	local x2 : SB + HALFSTROKE * 0.6
	local y2 : -XH * 0.05
	include : spiro
		widths.lhs
		g4 RIGHTSB (XH - HOOK)
		hookstart XH
		flat (SB + O) (XH - SMALLSMOOTHA)
		curl (SB + O) SMALLSMOOTHB
		arcvh
		CurlyTail fine rinner m1 0 RIGHTSB x2 y2

create-glyph 'ezhcurlytail' : glyph-construction
	assign-unicode 0x293
	include eMarks
	include : EzhShape XH DESCENDER nothing true
	local fine : adviceBlackness 3
	local rinner : (XH * 0.4 - fine * 1.5) / 2
	local m1 : RIGHTSB - STROKE * CORRECTION_HX
	local x2 : RIGHTSB - HALFSTROKE
	local y2 DESCENDER
	include : spiro
		widths.lhs
		flat m1 [mix [mix DESCENDER XH 0.6] DESCENDER 0.5] [heading DOWNWARD]
		curl m1 ([mix [mix DESCENDER XH 0.6] DESCENDER 0.5] - 1) [heading DOWNWARD]
		CurlyTail fine rinner m1 DESCENDER SB x2 y2

create-glyph 'zcurlytail' : glyph-construction
	assign-unicode 0x291
	include eMarks
	
	local cor 1.2
	local fine : adviceBlackness 4
	local x1 : mix (SB + STROKE) RIGHTSB 0.55
	local x2 : mix (SB + STROKE) RIGHTSB 0.3
	local y2 : -XH * 0.2
	local rinner : fine * 0.3
	local m1 : RIGHTSB - fine / 2 - rinner + O
	include : HCurlyTail fine STROKE rinner SB m1 (RIGHTSB + fine / 2) x1 x2 y2
	
	include : HBarTop SB RIGHTSB XH
	
	start-from SB STROKE
	line-to (SB + STROKE * cor) STROKE
	line-to RIGHTSB (XH - STROKE)
	line-to (RIGHTSB - STROKE * cor) (XH - STROKE)
	reverse-last

# Rhotic hook
define [ErTail left w dohook] : glyph-construction
	local right WIDTH
	local mid : mix left right 0.5
	local rise : (WIDTH - left) * 0.3
	include : spiro
		widths.center
		g2 (left - markFine * 2 * CORRECTION_HX) (XH * 0.5)
		g2 (mid - markFine * 2 * CORRECTION_HX) (XH * 0.5 + rise) [widths 0 STROKE]
	include : spiro
		widths.rhs [fallback w (markFine * 2)]
		flat mid (XH * 0.5 + rise) [heading DOWNWARD]
		curl mid (XH * 0.5 + [if dohook 0 (rise - 1)]) [heading DOWNWARD]
		if dohook {[hookend (XH * 0.5 - rise)]} {[arcvh]}
		g4 (right - [if dohook (markFine * 2) 0]) (XH * 0.5 - [if dohook (rise * 0.5) rise]) [if dohook nothing [heading RIGHTWARD]]
		#if dohook {[hookend (XH * 0.5 - rise)] [g4 right (XH * 0.5)]} {}

create-glyph 'rhotichook' : glyph-construction
	assign-unicode 0x2DE
	include : ErTail 0 STROKE true
create-glyph 'er' : glyph-construction
	assign-unicode 0x25A
	include eMarks
	local f : Thinner {'schwa'} 0.85
	include f.schwa
	include : ErTail (WIDTH * 0.85 - SB)

create-glyph 'revlatinepsiloner' : glyph-construction
	assign-unicode 0x25D
	include eMarks
	local f : Thinner {'revlatinepsilon'} 0.85
	include f.revlatinepsilon
	include : ErTail (WIDTH * 0.85 - SB)

# Belted
define [Belt] : glyph-construction
	local fine : adviceBlackness 4.5
	local r : XH * 0.05
	include : create-stroke
		:.start-from MIDDLE (CAPMIDDLE + r)
		:.set-width  0 fine
		:.heads-to   LEFTWARD
		:.line-to    (MIDDLE - HALFSTROKE - r * 1.5) (CAPMIDDLE + r)
		:.arc-hv-to  (MIDDLE - HALFSTROKE - r * 2.5) CAPMIDDLE
		:.arc-vh-to  (MIDDLE - HALFSTROKE - r * 1.5) (CAPMIDDLE - r)
		:.line-to    (MIDDLE + HALFSTROKE + r * 1.5 + fine) (CAPMIDDLE - r)

create-glyph 'lbelt' : glyph-construction
	assign-unicode 0x26C
	include glyphs.'l.serifed' AS_BASE
	include : Belt

create-glyph 'lbeltrtail' : glyph-construction
	assign-unicode 0xA78E
	include glyphs.lrtail AS_BASE
	include : Belt

create-glyph 'ltildeover' : glyph-construction
	assign-unicode 0x26B
	include glyphs.'l.serifed' AS_BASE
	include : create-glyph : glyph-construction
		include glyphs.tildeAbove
		apply-transform : Upright
		apply-transform : Translate (MIDDLE - markMiddle) (CAP * 0.525 - [mix aboveMarkBot aboveMarkTop 0.5])
		apply-transform : Italify

# Double stack
if [not recursive]: create-glyph 'ww' : glyph-construction
	assign-unicode 0x2AC
	local p : Object.create para
	p.xheight = CAP / 2
	local forked : Fork {'w'} p
	include forked.w
	apply-transform : Upright
	apply-transform : Translate 0 (CAP / 2)
	apply-transform : Italify
	include forked.w

create-glyph 'bidentalpercussive' : glyph-construction
	assign-unicode 0x2AD
	local g : create-glyph : glyph-construction
		include : HBarTop SB RIGHTSB (CAP * 0.4)
		include : VBarLeft SB (CAP * 0.1) (CAP * 0.4)
		include : VBarRight RIGHTSB (CAP * 0.1) (CAP * 0.4)
	include g
	apply-transform : Upright
	apply-transform : Translate 0 (CAP / 2)
	apply-transform : Italify
	include g

# Turned glyphs
turned nothing 0x27A 'rlongleg' MIDDLE (XH / 2) [anchorDeriv bMarks {.anchors {.bottomright {.type BASE .x RIGHTSB - RBALANCE .y 0}}}]
turned nothing 0x2C79 'rrtail' MIDDLE (XH / 2)