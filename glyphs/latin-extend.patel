###### EXTENDED LATIN CHARACTERS

create-glyph 'AE' : glyph-construction {
	set-width WIDTH
	assign-unicode 0xC6
	include capitalMarks
	
	local sw : Math.min STROKE [[WIDTH - SB * 2] * 0.25]
	local eleft : MIDDLE - sw * 0.25
	local turn : XH * 0.1
	
	# A half
	
	include : create-stroke
		:.start-from SB 0
		:.heads-to   UPWARD
		:.set-width  0 sw
		:.line-to    SB turn
		:.heads-to   UPWARD
		:.curve-to   SB [mix turn CAP 0.27] [eleft - HALFSTROKE] CAP
		:.set-width  0 [sw * 0.8]
	
	include : create-stroke 
		:.start-from [SB + sw] [XH / 2]
		:.heads-to   RIGHTWARD
		:.set-width  0 sw
		:.line-to    [eleft + sw / 2] [XH / 2]
		:.heads-to   RIGHTWARD
	
	# E half
	include : create-stroke :.start-from eleft 0 :.heads-to UPWARD :.set-width 0 sw 
		:.line-to eleft CAP :.heads-to UPWARD
	include : create-stroke :.start-from [eleft - HALFSTROKE] CAP :.set-width 0 STROKE :.heads-to RIGHTWARD
		:.line-to RIGHTSB CAP :.heads-to RIGHTWARD
	include : create-stroke :.start-from eleft [CAP * 0.54] :.set-width HALFSTROKE HALFSTROKE :.heads-to RIGHTWARD
		:.line-to [RIGHTSB - sw / 4] [CAP * 0.54] :.heads-to RIGHTWARD
	include : create-stroke :.start-from eleft 0 :.set-width STROKE 0 :.heads-to RIGHTWARD
		:.line-to RIGHTSB 0 :.heads-to RIGHTWARD
}

create-glyph 'OE' : glyph-construction {
	set-width WIDTH
	assign-unicode 0x152
	include capitalMarks
	
	local sw : Math.min STROKE [[WIDTH - SB * 2] * 0.25]
	local eleft MIDDLE
	
	# O half
	
	include : create-stroke
		:.start-from [eleft + 1] CAP
		:.set-width  sw 0
		:.heads-to   LEFTWARD
		:.line-to    eleft CAP
		:.heads-to   LEFTWARD
		:.arc-hv-to  SB [CAP - SMOOTHA]
		:.line-to    SB SMOOTHB
		:.arc-vh-to  eleft 0
		:.heads-to   RIGHTWARD
		:.line-to    [eleft + 1] 0
		:.heads-to   RIGHTWARD
	
	# E half
	include : create-stroke :.start-from eleft 0 :.heads-to UPWARD :.set-width 0 sw 
		:.line-to eleft CAP :.heads-to UPWARD
	include : create-stroke :.start-from eleft CAP :.set-width 0 STROKE :.heads-to RIGHTWARD
		:.line-to RIGHTSB CAP :.heads-to RIGHTWARD
	include : create-stroke :.start-from eleft [CAP * 0.54] :.set-width HALFSTROKE HALFSTROKE :.heads-to RIGHTWARD
		:.line-to [RIGHTSB - sw / 4] [CAP * 0.54] :.heads-to RIGHTWARD
	include : create-stroke :.start-from eleft 0 :.set-width STROKE 0 :.heads-to RIGHTWARD
		:.line-to RIGHTSB 0 :.heads-to RIGHTWARD
}

create-glyph 'ae-epart' : glyph-construction {
	local sw : Math.min STROKE [[WIDTH - SB * 2] * 0.23]
	local eLeft : MIDDLE - sw / 2 * ITALICCOR
	local eMiddle : mix eLeft RIGHTSB 0.5
	local barbottom [XH * EBARPOS]
	
	local hookx [RIGHTSB - OXHOOK + TAILADJX * globalTransform.yx]
	local eHookMiddle [mix eLeft hookx 0.5]
	
	local sma : SMALLSMOOTHA * 0.6
	local smb : SMALLSMOOTHB * 0.6
	
	include : create-stroke
		:.start-from [RIGHTSB - O] barbottom
		:.heads-to   UPWARD
		:.set-width  sw 0
		:.line-to    [RIGHTSB - O] [XH - smb]
		:.arc-vh-to  eMiddle XO
		:.arc-hv-to  eLeft [XH - sma]
		:.line-to    eLeft smb
		:.arc-vh-to  eHookMiddle O
		:.curve-to   [mix eHookMiddle hookx KAPPA_HOOK] O hookx [SHOOK - TAILADJY * globalTransform.yx]
	include : create-stroke
		:.start-from [eLeft + sw / 2] barbottom
		:.set-width  sw 0
		:.heads-to   RIGHTWARD
		:.line-to    [RIGHTSB - sw / 2] barbottom
		:.heads-to   RIGHTWARD
}
create-glyph 'ae-apart' : glyph-construction {
	local sw : Math.min STROKE [[WIDTH - SB * 2] * 0.23]
	
	local bartop [XH * BARPOS + sw]
	local abarRight : MIDDLE + sw / 2 * ITALICCOR
	local m1        : mix SB abarRight 0.5
	local lowmiddle : mix SB [abarRight - sw] 0.7
	local barsmooth : mix SB abarRight 0.6
	
	local sma : SMALLSMOOTHA * 0.6
	local smb : SMALLSMOOTHB * 0.6
	
	include : create-stroke 
		:.start-from abarRight [XH - sma]
		:.set-width sw 0
		:.arc-vh-to m1 XO
		:.curve-to [mix m1 SB KAPPA_HOOK] XO [SB + OXHOOK] [XH - SHOOK]
	include : create-stroke
		:.start-from abarRight smb
		:.set-width 0 sw
		:.arc-vh-to lowmiddle O
		:.arc-hv-to [SB + O] [mix 0 bartop [smb / [sma + smb]]]
		:.arc-vh-to barsmooth bartop
		:.line-to abarRight bartop
		:.heads-to RIGHTWARD
}
create-glyph 'oe-opart' : glyph-construction {
	local sw : Math.min STROKE [[WIDTH - SB * 2] * 0.23]
	
	local bartop [XH * BARPOS + sw]
	local abarRight : MIDDLE + sw / 2 * ITALICCOR
	local m1        : mix SB abarRight 0.5
	local lowmiddle : mix SB [abarRight - sw] 0.7
	local barsmooth : mix SB abarRight 0.6
	
	local sma : SMALLSMOOTHA * 0.6
	local smb : SMALLSMOOTHB * 0.6
	
	include : create-stroke
		:.start-from abarRight smb
		:.set-width 0 sw
		:.arc-vh-to lowmiddle O
		:.arc-hv-to [SB + O] smb
		:.line-to   [SB + O] [XH - sma]
		:.arc-vh-to lowmiddle [XH - O]
		:.arc-hv-to abarRight [XH - smb]
		:.heads-to RIGHTWARD
}

create-glyph 'ae' : glyph-construction {
	set-width WIDTH
	assign-unicode 0xE6
	include eMarks
	include glyphs.ae-epart
	include glyphs.ae-apart
}
create-glyph 'oe' : glyph-construction {
	set-width WIDTH
	assign-unicode 0x153
	include eMarks
	include glyphs.ae-epart
	include glyphs.oe-opart
}

create-glyph 'Eth' : glyph-construction {
	assign-unicode 0xD0
	
	include glyphs.D BASE
	include : create-stroke
		:.start-from [SB * 0.3] [CAP * 0.54]
		:.set-width  HALFSTROKE HALFSTROKE
		:.heads-to   RIGHTWARD
		:.line-to    [mix [SB + STROKE] [RIGHTSB - STROKE] 0.4] [CAP * 0.54]
		:.heads-to   RIGHTWARD
}
create-glyph 'Dcroat' : glyph-construction {
	assign-unicode 0x110
	include glyphs.Eth BASE
}

create-glyph 'eth' : glyph-construction {
	set-width WIDTH
	assign-unicode 0xF0
	include bMarks
	
	include : smallo [CAP * 0.6] 0 SB RIGHTSB
	local ymiddlea : [CAP * 0.6 + SMALLSMOOTHA - SMALLSMOOTHB] / 2
	include : create-stroke
		:.start-from [RIGHTSB - O] ymiddlea
		:.set-width  STROKE 0
		:.curve-to   [RIGHTSB - O] [mix ymiddlea CAP 0.8] [SB + STROKE * 1.1] CAP
	
	include : create-stroke
		:.start-from [mix SB RIGHTSB 0.1] [mix XH CAP 0]
		:.set-width  HALFSTROKE HALFSTROKE
		:.line-to    [mix SB RIGHTSB 0.95] [mix XH CAP 0.4]
}
create-glyph 'dcroat' : glyph-construction {
	assign-unicode 0x111
	
	include glyphs.d BASE
	include : create-stroke
		:.start-from [mix [SB + STROKE] [RIGHTSB - STROKE] 0.6] [mix XH CAP 0.45]
		:.set-width  HALFSTROKE HALFSTROKE
		:.heads-to   RIGHTWARD
		:.line-to    [mix RIGHTSB WIDTH 0.7 ] [mix XH CAP 0.45]
		:.heads-to   RIGHTWARD
}