
###### NUMBERS

create-glyph 'zero.unslashed' : glyph-construction
	include capitalMarks
	set-width WIDTH
	include : smallo CAP 0 SB RIGHTSB

create-glyph 'zero.slashed' : glyph-construction
	include capitalMarks
	set-width WIDTH
	include glyphs.'zero.unslashed'
	local fine : 0.5 * OVERLAYSTROKE
	include : create-stroke
		:.start-from (SB + STROKE / 2) (CAP * (1 - 0.77))
		:.set-width  fine fine
		:.line-to    (RIGHTSB - STROKE / 2) (CAP * 0.77)

create-glyph 'zero.dotted' : glyph-construction
	include capitalMarks
	set-width WIDTH
	include glyphs.'zero.unslashed'
	local radius : Math.min DOTRADIUS ((RIGHTSB - SB - STROKE * 2) / 4)
	include : list
		Ring (CAPMIDDLE + radius) (CAPMIDDLE - radius) (MIDDLE + radius) (MIDDLE - radius)

select-variant 'zero' '0' 'slashed'


create-glyph 'one' : glyph-construction
	include capitalMarks
	set-width WIDTH
	assign-unicode '1'
	
	include : VBar (MIDDLE + ONEBALANCE) 0 CAP
	include : create-stroke
		:.start-from (MIDDLE - HALFSTROKE * CORRECTION_HX + ONEBALANCE) CAP
		:.set-width STROKE 0
		:.line-to (MIDDLE - HOOK * 1.5 + ONEBALANCE) (CAP - HOOK * 0.75)


create-glyph 'two' : glyph-construction
	include capitalMarks
	set-width WIDTH
	assign-unicode '2'
	
	local smb : SMOOTHB * 1
	include : spiro
		widths.rhs
		g4 SB (CAP - HOOK)
		hookstart (CAP - O)
		g4 RIGHTSB (CAP - smb)
		alsothru 0.5 0.45 [widths (ESS / 2) (ESS / 2)]
		flat SB 1 [widths.heading STROKE 0 DOWNWARD]
		curl SB 0 [heading DOWNWARD]
	
	include : HBarBottom  (SB + HALFSTROKE) RIGHTSB 0


create-glyph 'three' : glyph-construction
	include capitalMarks
	set-width WIDTH
	assign-unicode '3'
	
	local barcenter : CAP * 0.51
	local threeRadius : (CAPMIDDLE + HALFSTROKE - SMOOTH) * 1.25
	
	include : spiro
		widths.rhs
		g4 SB (CAP - HOOK)
		hookstart CAPO
		g4 RIGHTSB (CAP - (SMOOTHB * (CAP - barcenter) / CAPMIDDLE))
#		arcvh
		flat (RIGHTSB - threeRadius + 0.01) (barcenter - HALFSTROKE) [heading LEFTWARD]
		curl (RIGHTSB - threeRadius) (barcenter - HALFSTROKE) [heading LEFTWARD]
	include : spiro
		widths.lhs
		g4 SB HOOK
		hookstart O
		g4 RIGHTSB (SMOOTHA * barcenter / CAPMIDDLE)
#		arcvh
		flat (RIGHTSB - threeRadius + 0.01) (barcenter + HALFSTROKE) [heading LEFTWARD]
		curl (RIGHTSB - threeRadius) (barcenter + HALFSTROKE) [heading LEFTWARD]


create-glyph 'four' : glyph-construction
	include capitalMarks
	set-width WIDTH
	assign-unicode '4'
	
	local bar (CAP * 0.4)
	local vert : [mix SB RIGHTSB 0.85] - CORRECTION_HX * STROKE
	local p 0.25
	local fine (STROKE * 0.9)
	
	include : HBarTop SB RIGHTSB bar
	include : VBarLeft vert 0 CAP
	local slope : (CAP - bar) / (vert - fine * p * CORRECTION_HX - SB)
	include : spiro
		g4   SB bar [widths 0 fine]
		g4   (vert - fine * p * CORRECTION_HX) CAP [widths.heading 0 [Math.hypot fine (fine / slope)] UPWARD]

define [FiveShape top bp] : glyph-construction
	local ycurly : mix 0 (top * bp + HALFSTROKE) (SMOOTHA / (SMOOTHA + SMOOTHB))
	local xleft : SB + TBALANCE * (0.6 - globalTransform.yx * 2)
	
	include : spiro
		widths.rhs
		flat xleft (top * bp + HALFSTROKE) [heading RIGHTWARD]
		curl ([mix SB RIGHTSB 0.4] - CORRECTION_OMIDS) (top * bp + HALFSTROKE)
		g4 RIGHTSB ycurly
		hookend O
		g4 SB HOOK
	include : VBarLeft xleft (top * bp + HALFSTROKE) top
	include : HBarTop xleft (RIGHTSB - TBALANCE / 2) top

create-glyph 'five' : glyph-construction
	include capitalMarks
	set-width WIDTH
	assign-unicode '5'
	include : FiveShape CAP FIVEBARPOS


create-glyph 'six' : glyph-construction
	include capitalMarks
	set-width WIDTH
	assign-unicode '6'
	
	include : smallo (CAP * 0.6) 0 SB RIGHTSB
	local ymiddlea : (CAP * 0.6 - SMALLSMOOTHA + SMALLSMOOTHB) / 2
	include : create-stroke
		:.start-from (SB + O) ymiddlea
		:.set-width  0 STROKE
		:.curve-to   (SB + O) [mix ymiddlea CAP 0.8] (RIGHTSB - STROKE * 1.1) CAP


create-glyph 'seven' : glyph-construction
	include capitalMarks
	set-width WIDTH
	assign-unicode '7'
	
	include : create-stroke
		:.start-from SB CAP :.heads-to RIGHTWARD :.set-width 0 STROKE
		:.line-to RIGHTSB CAP :.heads-to RIGHTWARD
	
	local cor 1.15
	local x : mix SB RIGHTSB 0.15
	
	start-from x 0
	line-to (x + STROKE * cor) 0
	line-to RIGHTSB (CAP - STROKE)
	line-to (RIGHTSB - STROKE * cor) (CAP - STROKE)
	reverse-last


create-glyph 'eight' : glyph-construction
	include capitalMarks
	set-width WIDTH
	assign-unicode '8'
	
	local sm : SMOOTH * 0.975
	
	local p 0.96
	
	include : spiro
		widths.lhs
		g4 (MIDDLE) (CAP - O - STROKE) [heading RIGHTWARD]
		archv 1
		g4 ([mix SB RIGHTSB p] - STROKE) (CAP - sm * p) [heading DOWNWARD]
		alsothru 0.5 0.5 [widths HALFSTROKE HALFSTROKE]
		g4 (SB + STROKE) sm [widths.heading 0 STROKE DOWNWARD]
		arcvh 1
		g4 (MIDDLE) (O + STROKE) [heading RIGHTWARD]
		archv 1
		g4 (RIGHTSB - STROKE) sm [widths.heading 0 STROKE UPWARD]
		alsothru 0.5 0.5 [widths HALFSTROKE HALFSTROKE]
		g4 ([mix RIGHTSB SB p] + STROKE) (CAP - sm * p) [widths.heading STROKE 0 UPWARD]
		arcvh 1
		close

# There is an "eight without lower contour" shape used for /propto
create-glyph 'rotetedpropto' : glyph-construction
	local sma : SMOOTHA * 0.975 + CORRECTION_VS * CORRECTION_HX
	local smb : SMOOTHB * 0.975 - CORRECTION_VS * CORRECTION_HX
	
	local p 0.96
	local py 0.6
	
	include : spiro
		flat (RIGHTSB - STROKE) 0 [widths 0 STROKE]
		curl (RIGHTSB - STROKE) 1 [widths 0 STROKE]
		alsothru 0.5 py [widths (STROKE * py) (STROKE * (1 - py))]
		g4 ([mix RIGHTSB SB p] + STROKE) (CAP - sma * p) [widths STROKE 0]
		arcvh 1
		g4 (MIDDLE - CORRECTION_OMIDS + CORRECTION_VS) (CAP - O - STROKE)
		archv 1
		g4 ([mix SB RIGHTSB p] - STROKE) (CAP - smb * p)
		alsothru 0.5 (1 - py) [widths (STROKE * py) (STROKE * (1 - py))]
		flat (SB + STROKE) 1 [widths 0 STROKE]
		curl (SB + STROKE) 0 [widths 0 STROKE]

create-glyph 'nine' : glyph-construction
	include capitalMarks
	set-width WIDTH
	assign-unicode '9'

	local u CAP
	local d (CAP * 0.425 - HALFSTROKE)
	local l SB
	local r RIGHTSB
	local ymiddlea : mix u d 0.5
	local p 0.5
	include : spiro
		widths.rhs (STROKE * p)
		flat (r - STROKE * (1 - p) * CORRECTION_HX) ymiddlea
		curl (r - STROKE * (1 - p) * CORRECTION_HX) (d + SMALLSMOOTHA)
		arcvh
		g4   ([mix l r 0.5] + CORRECTION_OMIDS) (d + O) [widths 0 STROKE]
		archv
		flat l (d + SMALLSMOOTHB)
		curl l (u - SMALLSMOOTHA)
		arcvh
		g4   ([mix l r 0.5] - CORRECTION_OMIDS) (u - O)
		archv
		flat RIGHTSB ymiddlea
		curl RIGHTSB (CAP * 0.35)
		hookend O
		g4 SB HOOK


create-glyph 'ten' : glyph-construction
	include capitalMarks
	set-width : WIDTH * 2
	include glyphs.one
	apply-transform : Translate (-WIDTH) 0
	include glyphs.zero
	apply-transform : Translate WIDTH 0
