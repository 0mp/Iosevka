
###### NUMBERS

create-glyph 'zero.unslashed' : glyph-construction
	include capitalMarks
	set-width WIDTH
	include : OShape CAP 0 SB RIGHTSB

create-glyph 'zero.slashed' : glyph-construction
	include capitalMarks
	set-width WIDTH
	include glyphs.'zero.unslashed'
	local fine : 0.5 * OVERLAYSTROKE
	include : dispiro
		flat (SB + STROKE / 2) (CAP * (1 - 0.77)) [widths fine fine]
		curl (RIGHTSB - STROKE / 2) (CAP * 0.77)

create-glyph 'zero.dotted' : glyph-construction
	include capitalMarks
	set-width WIDTH
	include glyphs.'zero.unslashed'
	local radius : Math.min DOTRADIUS ((RIGHTSB - SB - STROKE * 2) / 4)
	include : Ring (CAPMIDDLE + radius) (CAPMIDDLE - radius) (MIDDLE + radius) (MIDDLE - radius)

select-variant 'zero' '0' 'slashed'


create-glyph 'one' : glyph-construction
	include capitalMarks
	set-width WIDTH
	assign-unicode '1'
	
	include : VBar (MIDDLE + ONEBALANCE) 0 CAP
	include : dispiro
		flat (MIDDLE - HALFSTROKE * CORRECTION_HX + ONEBALANCE) CAP [widths STROKE 0]
		curl (MIDDLE - HOOKX * 1.25 + ONEBALANCE) (CAP - HOOK * 0.75)
	
	if SLAB : begin
		include : CenterBottomSerif (MIDDLE + ONEBALANCE) 0 LONGJUT


create-glyph 'two' : glyph-construction
	include capitalMarks
	set-width WIDTH
	assign-unicode '2'
	
	define smooth [adviceGlottalStopSmooth CAP 1]
	include : dispiro
		widths.rhs
		g4 SB (CAP - HOOK)
		hookstart (CAP - O)
		g4 RIGHTSB (CAP - smooth)
		alsothru 0.5 0.425 [widths (ESS / 2) (ESS / 2)]
		flat SB 1 [widths.heading STROKE 0 DOWNWARD]
		curl SB 0 [heading DOWNWARD]
	
	include : HBarBottom  (SB + HALFSTROKE) RIGHTSB 0
	if SLAB : begin
		include : UpwardRightSerif RIGHTSB 0 VJUT


create-glyph 'three' : glyph-construction
	include capitalMarks
	set-width WIDTH
	assign-unicode '3'
	
	local barcenter : CAP * 0.51
	local threeRadius : (CAPMIDDLE + HALFSTROKE - SMOOTH) * 1.25
	
	include : dispiro
		widths.rhs
		g4 SB (CAP - HOOK)
		hookstart CAPO
		g4 RIGHTSB (CAP - (SMOOTHB * (CAP - barcenter) / CAPMIDDLE))
#		arcvh
		flat (RIGHTSB - threeRadius + 0.01) (barcenter - HALFSTROKE) [heading LEFTWARD]
		curl (RIGHTSB - threeRadius) (barcenter - HALFSTROKE) [heading LEFTWARD]
	include : dispiro
		widths.lhs
		g4 SB HOOK
		hookstart O
		g4 RIGHTSB (SMOOTHA * barcenter / CAPMIDDLE)
#		arcvh
		flat (RIGHTSB - threeRadius + 0.01) (barcenter + HALFSTROKE) [heading LEFTWARD]
		curl (RIGHTSB - threeRadius) (barcenter + HALFSTROKE) [heading LEFTWARD]


create-glyph 'four' : glyph-construction
	include capitalMarks
	set-width WIDTH
	assign-unicode '4'
	
	local bar (CAP * 0.4)
	local vert : [mix SB RIGHTSB 0.825] - CORRECTION_HX * STROKE
	local p 0.25
	local fine (STROKE * 0.9)
	
	include : HBarTop (SB + OX) RIGHTSB bar
	include : VBarLeft vert 0 CAP
	local slope : (CAP - bar) / (vert - fine * p * CORRECTION_HX - (SB + OX))
	include : dispiro
		g4   (SB + OX) bar [widths 0 fine]
		g4   (vert - fine * p * CORRECTION_HX) CAP [widths.heading 0 [Math.hypot fine (fine / slope)] UPWARD]
	if SLAB : begin
		include : CenterBottomSerif (vert + HALFSTROKE * CORRECTION_HX) 0 JUT

define [FiveShape top bp] : glyph-construction
	local t1 : top * bp * 0.8
	local t2 : top * bp
	local ycurly : mix 0 t2 (SMOOTHA / (SMOOTHA + SMOOTHB))
	local xleft : mix SB RIGHTSB 0.025
	local xright : [mix RIGHTSB SB 0.05] - OXE
	
	include : dispiro
		widths.rhs
		g4 (xleft - OXHOOK) (t2 - AHOOK * top / CAP)
		hookstart (t2 - O)
		g4 (RIGHTSB - OX) ycurly
		hookend O
		g4 SB HOOK
	include : VBarLeft xleft (t2 - AHOOK * top / CAP) top
	include : HBarTop xleft xright top
	if SLAB : begin
		include : DownwardRightSerif xright top VJUT

create-glyph 'five' : glyph-construction
	include capitalMarks
	set-width WIDTH
	assign-unicode '5'
	include : FiveShape CAP FIVEBARPOS


create-glyph 'six' : glyph-construction
	include capitalMarks
	set-width WIDTH
	assign-unicode '6'
	
	include : OShape (CAP * 0.6) 0 SB RIGHTSB
	local ymiddlea : (CAP * 0.6 - SMALLSMOOTHA + SMALLSMOOTHB) / 2
	include : dispiro
		widths.rhs
		g4.up.start (SB + OX) ymiddlea
		quadcontrols 0 0.8
		g4 (RIGHTSB - STROKE * 1.1) CAP


create-glyph 'seven' : glyph-construction
	include capitalMarks
	set-width WIDTH
	assign-unicode '7'
	
	include : HBarTop (SB + OXE) RIGHTSB CAP
	
	local x : mix SB RIGHTSB 0.28
	local cor : [Math.hypot 1 ((RIGHTSB - x - HALFSTROKE) / (CAP - STROKE))] * CORRECTION_HX
	
	start-from (x - STROKE * cor / 2) 0
	line-to (x + STROKE * cor / 2) 0
	line-to RIGHTSB (CAP - STROKE)
	line-to (RIGHTSB - STROKE * cor) (CAP - STROKE)
	reverse-last
	
	if SLAB : begin
		include : DownwardLeftSerif (SB + OXE) CAP VJUT


create-glyph 'eight' : glyph-construction
	include capitalMarks
	set-width WIDTH
	assign-unicode '8'
	
	local sma : SMOOTHA * 0.975
	local smb : SMOOTHB * 0.975
	
	local p 0.96
	local l (SB + OX)
	local r (RIGHTSB - OX)
	
	include : dispiro
		widths.lhs
		g4.right.mid (MIDDLE - CORRECTION_OMIDS) (CAP - O - STROKE)
		archv 1
		g4 ([mix l r p] - STROKE) (CAP - sma * p) [heading DOWNWARD]
		alsothru 0.5 0.5 [widths HALFSTROKE HALFSTROKE]
		g4 (l + STROKE) sma [widths.heading 0 STROKE DOWNWARD]
		arcvh 1
		g4.right.mid (MIDDLE + CORRECTION_OMIDS) (O + STROKE)
		archv 1
		g4 (r - STROKE) smb [widths.heading 0 STROKE UPWARD]
		alsothru 0.5 0.5 [widths HALFSTROKE HALFSTROKE]
		g4 ([mix r l p] + STROKE) (CAP - smb * p) [widths.heading STROKE 0 UPWARD]
		arcvh 1
		close

# There is an "eight without lower contour" shape used for /propto
create-glyph 'rotetedpropto' : glyph-construction
	local sma : SMOOTHA * 0.975 + CORRECTION_VS * CORRECTION_HX
	local smb : SMOOTHB * 0.975 - CORRECTION_VS * CORRECTION_HX
	
	local p 0.96
	local py 0.6
	local l (SB + OX)
	local r (RIGHTSB - OX)
	include : dispiro
		flat (r - STROKE) 0 [widths 0 STROKE]
		curl (r - STROKE) 1 [widths 0 STROKE]
		alsothru 0.5 py [widths (STROKE * py) (STROKE * (1 - py))]
		g4 ([mix r l p] + STROKE) (CAP - sma * p) [widths STROKE 0]
		arcvh 1
		g4 (MIDDLE - CORRECTION_OMIDS + CORRECTION_VS) (CAP - O - STROKE)
		archv 1
		g4 ([mix l r p] - STROKE) (CAP - smb * p)
		alsothru 0.5 (1 - py) [widths (STROKE * py) (STROKE * (1 - py))]
		flat (l + STROKE) 1 [widths 0 STROKE]
		curl (l + STROKE) 0 [widths 0 STROKE]

create-glyph 'nine' : glyph-construction
	include capitalMarks
	set-width WIDTH
	assign-unicode '9'

	local u CAP
	local d (CAP * 0.425 - HALFSTROKE)
	local l (SB + OX)
	local r (RIGHTSB - OX)
	local ymiddlea : mix u d 0.5
	local p 0.5
	include : dispiro
		widths.rhs (STROKE * p)
		flat (r - STROKE * (1 - p) * CORRECTION_HX) ymiddlea
		curl (r - STROKE * (1 - p) * CORRECTION_HX) (d + SMALLSMOOTHA)
		arcvh
		g4   ([mix l r 0.5] + CORRECTION_OMIDS) (d + O) [widths 0 STROKE]
		archv
		flat l (d + SMALLSMOOTHB)
		curl l (u - SMALLSMOOTHA)
		arcvh
		g4   ([mix l r 0.5] - CORRECTION_OMIDS) (u - O)
		archv
		flat r ymiddlea
		curl r (CAP * 0.35)
		hookend O
		g4 SB HOOK


create-glyph 'ten' : glyph-construction
	include capitalMarks
	set-width : WIDTH * 2
	include glyphs.one
	apply-transform : Translate (-WIDTH) 0
	include glyphs.zero
	apply-transform : Translate WIDTH 0
