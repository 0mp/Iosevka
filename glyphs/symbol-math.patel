###### Mathematical symbols

### Letter-like
alias 'micro' 0xB5 'mu'
turned 'forall' 0x2200 'A' MIDDLE [CAP / 2]
create-glyph 'exists' : glyph-construction {
	assign-unicode 0x2203
	include : FShape 0.5
	include : HBarBottom [SB * 1.5 - O] RIGHTSB 0
	include : FlipAround MIDDLE [CAP / 2]
}
alias 'increment' 0x2206 'Delta'
turned 'nabla' 0x2207 'increment' MIDDLE [CAP / 2]
create-glyph 'sum' : glyph-construction {
	assign-unicode 0x2211
	include : SigmaShape parenTop parenBot
}
create-glyph 'product' : glyph-construction {
	assign-unicode 0x220F
	include : PiShape parenTop parenBot 0
}
turned 'coproduct' 0x2210 'product' MIDDLE parenMid

### Others

alias 'minus' 0x2212 'hyphen'
create-glyph 'plusminus' : glyph-construction {
	assign-unicode 0xB1
	set-width WIDTH
	include glyphs.underscore
	include glyphs.plus
}
create-glyph 'multiply' : glyph-construction {
	assign-unicode 0xD7
	local radius : [RIGHTSB - SB] / 2
	include : create-stroke
		:.start-from [MIDDLE - radius] [parenMid - radius]
		:.set-width  HALFSTROKE HALFSTROKE
		:.line-to    [MIDDLE + radius] [parenMid + radius]
	
	include : create-stroke
		:.start-from [MIDDLE + radius] [parenMid - radius]
		:.set-width  HALFSTROKE HALFSTROKE
		:.line-to    [MIDDLE - radius] [parenMid + radius]
}
create-glyph 'divide' : glyph-construction {
	assign-unicode 0xF7
	include glyphs.hyphen
	local radius : [RIGHTSB - SB] / 2
	include : list {
		Ring [parenMid + radius + DOTRADIUS] [parenMid + radius - DOTRADIUS] [MIDDLE - DOTRADIUS + O] [MIDDLE + DOTRADIUS - O]
		Ring [parenMid - radius + DOTRADIUS] [parenMid - radius - DOTRADIUS] [MIDDLE - DOTRADIUS + O] [MIDDLE + DOTRADIUS - O]
	}
}
create-glyph 'negate' : glyph-construction {
	assign-unicode 0xAC
	
	include glyphs.hyphen
	
	include : VBar [RIGHTSB - HALFSTROKE] [parenMid - [RIGHTSB - SB] * 0.55] [parenMid + HALFSTROKE]
}

alias 'mathbullet' 0x2219 'bullet'
create-glyph 'sqrt' : glyph-construction {
	assign-unicode 0x221A
	
	include : create-stroke
		:.start-from SB [mix parenBot parenTop 0.45]
		:.set-width  HALFSTROKE HALFSTROKE
		:.line-to    MIDDLE parenBot
		:.heads-to   DOWNWARD
		:.set-samples    1
	
	include : create-stroke
		:.start-from WIDTH parenTop
		:.set-width  HALFSTROKE HALFSTROKE
		:.line-to    MIDDLE parenBot
		:.heads-to   DOWNWARD
		:.set-samples    1
}

create-glyph 'integrate' : glyph-construction {
	assign-unicode 0x222B
	include : LongSShape parenTop parenBot HOOK [HOOK * 0.75]
}
dual 'doubleintegrate' 0x222C 'integrate' [0.5 * WIDTH]